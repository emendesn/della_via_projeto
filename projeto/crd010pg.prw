#INCLUDE "protheus.ch"
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออหออออออัอออออออออออออออออออออหอออออัออออออออออปฑฑ
ฑฑบPrograma  ณ Crd010PG        บAutor ณ Paulo Benedet       บData ณ 28/06/05 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออสออออออฯอออออออออออออออออออออสอออออฯออออออออออนฑฑ
ฑฑบDescricao ณ Ponto de entrada                                              บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณ ParamIxb[1] - 2 - visualizacao                                บฑฑ
ฑฑบ          ณ               3 - inclusao                                    บฑฑ
ฑฑบ          ณ               4 - alteracao                                   บฑฑ
ฑฑบ          ณ               5 - exclusao                                    บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nao ha                                                        บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAplicacao ณ Ponto de entrada do programa CRDA010 apos a gravacao dos      บฑฑ
ฑฑบ          ณ arquivos.                                                     บฑฑ
ฑฑฬออออออออออุออออออออัออออออัอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณ Data   ณBops  ณManutencao Efetuada                            บฑฑ
ฑฑฬออออออออออุออออออออุออออออุอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบBenedet   ณ28/06/05ณ      ณGravar informacoes de credito segundo PL26.    บฑฑ
ฑฑบ          ณ        ณ      ณ                                               บฑฑ
ฑฑบNorbert   ณ13/07/05ณ      ณGravacao dos dados de referencia do cliente    บฑฑ
ฑฑบ          ณ        ณ      ณ                                               บฑฑ
ฑฑบBenedet   ณ17/09/05ณ      ณVerificacao do programa que esta chamando o    บฑฑ
ฑฑบ          ณ        ณ      ณponto de entrada.                              บฑฑ
ฑฑฬออออออออออุออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ13797 - Della Via Pneus                                        บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
User Function CRD010PG()

If FunName() == "P_DELA037" .Or. FunName() == "CRDA010"
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณBenedet - Gravacao dos dados de credito do cliente. (Valor e Dt de Validade)ณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If ParamIxb[1] == 3 .Or. ParamIxb[1] == 4
		P_MontaCred()
	EndIf
EndIf

If FunName() == "P_DELA037"
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณNorbert - Gravacao dos dados das referencias do clienteณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	SAOGrava()
EndIf

Return

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัอออออออออออออออออหออออออัอออออออออออออออออออออหอออออัออออออออออปฑฑ
ฑฑบPrograma  ณ SAOGrava        บAutor ณNorbert Waage Junior บData ณ 13/07/05 บฑฑ
ฑฑฬออออออออออุอออออออออออออออออสออออออฯอออออออออออออออออออออสอออออฯออออออออออนฑฑ
ฑฑบDescricao ณGravacao das referencias do cliente                            บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณNao se aplica                                                  บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณ Nao ha                                                        บฑฑ
ฑฑฬออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAplicacao ณ Rotina executada pelo ponto de entrada CRD010PG para gravar asบฑฑ
ฑฑบ          ณ informacoes digitadas pelo usuario na tela de referencias.    บฑฑ
ฑฑฬออออออออออุออออออออัออออออัอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณ Data   ณBops  ณManutencao Efetuada                            บฑฑ
ฑฑฬออออออออออุออออออออุออออออุอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ          ณ        ณ      ณ                                               บฑฑ
ฑฑฬออออออออออุออออออออฯออออออฯอออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ13797 - Della Via Pneus                                        บฑฑ
ฑฑศออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function SAOGrava()

Local aArea		:=	GetArea()
Local aCposCh	:=	{}
Local lInclui	:=	(ParamIxb[1] == 3)
Local lAltera	:=	(ParamIxb[1] == 4)
Local lExclui	:=	(ParamIxb[1] == 5)

If lAltera .Or. lInclui
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDados de Instituicoesณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCposCh	:=	{}
	
	AAdd(aCposCh,{"AO_FILIAL"	, xFilial("SAO")})
	AAdd(aCposCh,{"AO_TIPO"		, "1"			})
	AAdd(aCposCh,{"AO_CLIENTE"	, SA1->A1_COD	})
	AAdd(aCposCh,{"AO_LOJA"		, SA1->A1_LOJA	})
	
	P_GetToFile("SAO",1,_aHead037[1],_aCols037[1],_aRecn037[1],aCposCh)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDados de Empresasณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCposCh	:= {}
	
	AAdd(aCposCh,{"AO_FILIAL"	, xFilial("SAO")})
	AAdd(aCposCh,{"AO_TIPO"		, "2"			})
	AAdd(aCposCh,{"AO_CLIENTE"	, SA1->A1_COD	})
	AAdd(aCposCh,{"AO_LOJA"		, SA1->A1_LOJA	})
	
	P_GetToFile("SAO",1,_aHead037[2],_aCols037[2],_aRecn037[2],aCposCh)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณDados de Bancosณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	aCposCh	:= {}
	
	AAdd(aCposCh,{"AO_FILIAL"	, xFilial("SAO")})
	AAdd(aCposCh,{"AO_TIPO"		, "3"			})
	AAdd(aCposCh,{"AO_CLIENTE"	, SA1->A1_COD	})
	AAdd(aCposCh,{"AO_LOJA"		, SA1->A1_LOJA	})
	
	P_GetToFile("SAO",1,_aHead037[3],_aCols037[3],_aRecn037[3],aCposCh)
	
ElseIf lExclui
	
	DbSelectArea("SAO")
	DbSetOrder(1)//AO_FILIAL+AO_CLIENTE+AO_LOJA+AO_TIPO
	DbSeek(xFilial("SAO") + M->A1_COD + M->A1_LOJA)
	
	While !Eof() .And. (SAO->(AO_FILIAL+AO_CLIENTE+AO_LOJA) == xFilial("SAO") + M->A1_COD + M->A1_LOJA)
		
		RecLock("SAO",.F.)
		DbDelete()
		MsUnLock()
		DbSkip()
		
	End
	
EndIf

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณGetToFile บAutor  ณNorbert Waage Juniorบ Data ณ  07/07/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณDescarrega o conteudo da GetDados em uma area de trabalho   บฑฑ
ฑฑบ          ณParametros:                                                 บฑฑ
ฑฑบ          ณcAlias   - Area de trabalho que recebera os campos          บฑฑ
ฑฑบ          ณnIndice  - Numero do indice a ser considerado               บฑฑ
ฑฑบ          ณaH       - aHeader da GetDados relacionada                  บฑฑ
ฑฑบ          ณaC       - aCols da GetDados relacionada                    บฑฑ
ฑฑบ          ณaRecnos  - Recnos dos itens da getdados                     บฑฑ
ฑฑบ          ณaCposChv --Campos a serem gravados alem dos campos do aCols บฑฑ
ฑฑบ          ณ         ภ-Estrutura aCposChv:  {{CAMPO,VALOR},...}         บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGenerico                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Project Function GetToFile(cAlias,nIndice,aH,aC,aRecnos,aCposChv)

Local aArea		:=	GetArea()
Local nPosDel	:=	Len(aH) + 1
Local nRegs		:=	Len(aRecnos)
Local nPos,nCampo

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณAbre area de trabalhoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
DbSelectArea(cAlias)
DbSetOrder(nIndice)

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณPercorre os campos do aColsณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
For nPos := 1 to Len(aC)
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe o item nao estiver deletado e estiver preenchidoณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !aC[nPos][nPosDel] .And. EmptyCols(nPos,aC)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณVerifica se o item atual ja se encontrava no bancoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nPos > nRegs
			RecLock(cAlias,.T.)
		Else
			DbGoTo(aRecnos[nPos])
			RecLock(cAlias,.F.)
		EndIf
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณAtualizacao dos camposณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		(cAlias)->(&(cAlias+"_FILIAL")) := xFilial(cAlias)
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณGrava campos que nao estao no aHeaderณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nCampo := 1 to Len(aCposChv)
			(cAlias)->(&(aCposChv[nCampo][1]))	:= aCposChv[nCampo][2]
		Next nCampo
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณPercorre os campos do aHeaderณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		For nCampo := 1 to Len(aH)
			
			//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
			//ณGrava os campos de contexto realณ
			//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
			If (cAlias)->(FieldPos(aH[nCampo][2])) > 0
				(cAlias)->(&(aH[nCampo][2]))	:=	aC[nPos][nCampo]
			EndIf
			
		Next nCampo
		
		MsUnLock()
		
	Else
		
		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณTrata a Delecaoณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If nPos <= nRegs
			
			DbGoTo(aRecnos[nPos])
			RecLock(cAlias,.F.)
			DbDelete()
			MsUnLock()
			
		EndIf
		
	EndIf
	
Next nPos

RestArea(aArea)

Return Nil

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณEmptyCols บAutor  ณNorbert Waage Juniorบ Data ณ  07/07/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para verificar se o aCols foi preenchido             บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณGenerico                                                    บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function EmptyCols(n,aCols)

Local lRet:=.T.,lVazio:=.T.

If n==Len(aCols)
	Aeval(aCols[n],{|x|If(lVazio,lVazio:=Empty(x),lVazio)})
	If lVazio
		lRet:=.F.
	EndIf
EndIf

Return (lRet)
