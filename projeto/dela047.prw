#INCLUDE "protheus.ch"
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณDELA047   บ Autor ณNorbert Waage Juniorบ Data ณ  17/08/05   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDescricao ณRotina responsavel pelo tratamento de descontos na venda as-บฑฑ
ฑฑบ          ณsitida, respeitando o valor de desconto estabelecido no ca- บฑฑ
ฑฑบ          ณdastrp de descontos(PAN,PAO).                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบParametrosณNao se aplica                                               บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบRetorno   ณlRet - (.T.) Valida a alteracao do desconto sempre, pois    บฑฑ
ฑฑบ          ณa negacao permite que se retorne ao valor antigo, caso o    บฑฑ
ฑฑบ          ณusuario pressione ESC.                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAplicacao ณRotina acionada apos a digitacao do desconto ou do valor do บฑฑ
ฑฑบ          ณdesconto.                                                   บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ 13797 - Della Via Pneus                                    บฑฑ
ฑฑฬออออออออออุออออออออัออออออัออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบAnalista  ณData    ณBops  ณManutencao Efetuada                   	  บฑฑ
ฑฑบNorbert   ณ11/01/06ณ------ณTratamento de decimais no desconto em valor.บฑฑ
ฑฑบ          ณ        ณ      ณ                                            บฑฑ
ฑฑศออออออออออฯออออออออฯออออออฯออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Project Function DELA047()

Local lRet 		:= .T.
Local aOpcErr	:= {"este grupo de produto.","esta loja."}
Local cProduto	:= aCols[N,aScan(aHeader,{|x| AllTrim(x[2]) == "LR_PRODUTO"})]
Local nVlrItem	:= aCols[N,aScan(aHeader,{|x| AllTrim(x[2]) == "LR_VLRITEM"})]
Local cGrupo	:= GetAdvFVal("SB1","B1_GRUPO",xFilial("SBM")+cProduto,1,"")
Local nOpcErr	:= 0
Local nPerDscLj	:= GetAdvFVal("PAN","PAN_DESCLJ",xFilial("PAN")+cFilAnt,1,0)
Local nPerDscGr	:= GetAdvFVal("PAO","PAO_DESC",xFilial("PAO")+cFilAnt+cGrupo,1,0)
Local nPerDesc	:= IIF(nPerDscGr == 0,(nOpcErr:=1,nPerDscLj),(nOpcErr:=2,nPerDscGr))
Local nVlrDesc	:= &(ReadVar())
Local nTolera	:= (1/(10 ** nDecimais))/2
Local nDif		:= 0

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ********************** IMPORTANTE ***********************ณ
//ณAdmite-se uma tolerancia de meia unidade decimal         ณ
//ณ(0.005 p/ nDecimais = 2)                                 ณ
//ณUsa-se somente o simbolo '<' e nao '<=' para que tenha-seณ
//ณcerteza de que o percentual de desconto nao sera         ณ
//ณarredondado para um decimal acima.                       ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSe a rotina foi acionada pelo valor de descontoณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If AllTrim(ReadVar()) == "M->LR_VALDESC"
	
	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe o desconto for maior que o permitido, zera variaveisณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
	If !(((nVlrDesc/(nVlrItem + nVlrDesc)) * 100) <= nPerDesc )

		nDif	:= Abs(nPerDesc - ((nVlrDesc/(nVlrItem + nVlrDesc)) * 100))

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe ainda com tolerancia o desconto ultrapassa o permitido,ณ
		//ณnao permite o desconto                                    ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !(nDif < nTolera)
			lRet := .F.
			M->LR_VALDESC := 0
		Endif

	EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณSe a rotina foi acionada pelo % de desconto ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
ElseIf AllTrim(ReadVar()) == "M->LR_DESC"

	//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
	//ณSe o desconto for maior que o permitido, zera variaveisณ
	//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู            
	If !( nVlrDesc <= nPerDesc )
	
		nDif	:= Abs(nVlrDesc - nPerDesc)

		//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
		//ณSe ainda com tolerancia o desconto ultrapassa o permitido,ณ
		//ณnao permite o desconto                                    ณ
		//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
		If !(nDif < nTolera)
			lRet := .F.
			M->LR_DESC	 := 0
		EndIf

	EndIf

EndIf

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณNotifica o usuario e retorna .T.                               ณ
//ณ*NOTA: Se a rotina retornar .F., o campo selecionado permitira ณ
//ณque pressione-se ESC, retornando ao valor de desconto anterior,ณ
//ณporem sem que este tenha sido aplicado aos valores             ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
If !lRet                                                                                                
	aCols[N,aScan(aHeader,{|x| AllTrim(x[2]) == "LR_VALDESC"})] := 0
	aCols[N,aScan(aHeader,{|x| AllTrim(x[2]) == "LR_DESC"})] := 0
	oGetVA:oBrowse:Refresh()
	MsgAlert("O desconto aplicado ้ maior do que o desconto permitido para este grupo de produto","Desconto")
	lRet := .T.
EndIf

Return lRet