#Include "Protheus.Ch"
/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DELA015A  ºAutor  ³Ricardo Mansano     º Data ³  03/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºLocacao   ³ Fab.Tradicional  ³Contato ³ mansano@microsiga.com.br       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Atualiza Campos da aCols, aColsDet e Rodapes               º±±
±±º          ³ de acordo com Produtos ou Quantidades digitados            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³_nValor := Preco Unitario do Produto                        º±±
±±º          ³_lAtuBar:= Idica se os rodapes devem ser atualizados        º±±
±±º          ³_cCond  := Condicao de pagamento utilizada                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ 01-nVlrUnit = Valor tratado pela função P_Dela027  que     º±±
±±º          ³            o valor da tabela de preços em DA1 respeitando  º±±
±±º          ³            as mesmas regras do Faturamento.                º±±
±±º          ³ 02-Caso ja venha preenchido o Valor Unitario o sistema     º±±
±±º          ³    Ignora a rotina de busca de preco na tabela.            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ Gatilho dos campos:                                        º±±
±±º          ³ LR_PRODUTO      Sequencia :001  Contra Dominio : LR_VRUNIT º±±
±±º          ³ LR_QUANT        Sequencia :001  Contra Dominio : LR_VRUNIT º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 13797 - Dellavia Pneus                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAnalista  ³Data    ³Bops  ³Manutencao Efetuada                      	  º±±
±±º          ³        ³      ³                                         	  º±±
±±ºNorbert   ³23/06/05³      ³Substituicao da rotina MaTabPrVen pela roti-º±±
±±º          ³        ³      ³na P_DELA027 para calcular o preco de venda.º±±
±±ºNorbert   ³01/07/05³      ³Comentado o trecho onde os descontos sao ze-º±±
±±º          ³        ³      ³rados.                                      º±±
±±ºNorbert   ³11/07/05³      ³Tratamento para produtos que nao estao ca-  º±±
±±º          ³        ³      ³dastrados em tabela de preco.               º±±
±±ºNorbert   ³09/02/06³      ³Alteracao do calculo dos descontos. Foi uti-º±±
±±º          ³        ³      ³lizada a mesma logica do programa Lj7VlItem,º±±
±±º          ³        ³      ³do padrao (LOJA701A).                    	  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Project Function DELA015A(_nValor,_lAtuBar,cCond)

Local nX   			:= 0
Local nVlrUnit		:= 0
Local nVlrItem		:= 0
Local nVlrUnBr		:= 0
Local nVlrDesc		:= 0
Local nVlrPDesc		:= 0
Local nTot 			:= 0
Local cMoeda	  	:= 1
Local naCols		:= Len(aCols)
Local naColsItem	:= Len(aCols[n])
Local cTipoPrd		:= ""
Local cCondPg  		:= ""
Local nParc			:= 0
Local nTotSeg		:= 0
Local nTotCorr		:= 0
Local nDespMM 		:= 0 
Local nValDig		:= 0
Local aVlrMM  		:= {}
Local lAtuDet		:= .F.
Local lSaidaR		:= .F.
Local nPosProduto 	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_PRODUTO" })
Local nPosLocal   	:= aScan(aHeaderDet,{ |x| AllTrim(x[2]) == "LR_LOCAL"   })
Local nPosQuant   	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_QUANT"   })
Local nPosVlrItem 	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_VLRITEM" })
Local nPosVrUnit	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_VRUNIT"  })
Local nPosValDesc 	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_VALDESC" })
Local nPosPercDesc 	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_DESC"    })
Local nPosPrcTab  	:= aScan(aHeaderDet,{ |x| AllTrim(x[2]) == "LR_PRCTAB"  })
Local nPosPrcAtu  	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_PRCATU"  })
Local nPosMM      	:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_MM"      })
Local nPosTpOp		:= aScan(aHeader   ,{ |x| AllTrim(x[2]) == "LR_TPOPER"  })
Local nPosTES		:= aScan(aHeaderDet,{ |x| AllTrim(x[2]) == "LR_TES"     })
Local nPosDtBIcm	:= aPosCpoDet[05][2]				// Posicao da Base de ICM
Local nPosDtVIcm	:= aPosCpoDet[06][2]				// Posicao do Valor do ICM
Local nPosDtVIpi	:= aPosCpoDet[07][2]				// Posicao do Valor do IPI
Local nPosDtVIss 	:= aPosCpoDet[08][2]				// Posicao do Valor do ISS
Local _aArea   		:= {}
Local _aAlias  		:= {}

Default _lAtuBar	:= .T.

P_CtrlArea(1,@_aArea,@_aAlias,{"PAD"}) // GetArea 

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se eh saida rapida - Norbert - 15/12/05³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lSaidaR	:= AllTrim(GetAdvFVal("SB1","B1_GRUPO",xFilial("SB1")+aCols[N,nPosProduto],1,"")) == AllTrim(GetMv("FS_DEL002"))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Grava valor digitado no preco de tabela para itens de saida³
//³rapida - Norbert - 15/12/05                                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If	AllTrim(ReadVar()) == "M->LR_VRUNIT" .AND. lSaidaR

	aCols[N,nPosPrcAtu] := &(ReadVar())  //Preco Tabela
	
EndIf

// Impede digitação de Itens se Tab. de Preços ou Tipo de Venda estiver em branco
If (Alltrim(M->LQ_CODTAB)=="").or.(Alltrim(M->LQ_TIPOVND)=="")
	
	// Limpa produto para que usuário possa preencher a Tab. de Preços
	aCols[n,nPosProduto] := space(15)
	
	// Zera Valor para forçar digitação da Tab de Preços
	nVlrUnit := 0
	
	Aviso("Atenção !!!","Preencha o Cod. da Tabela de Preços e o Tipo de Venda !!!",{" << Voltar"},2,"Tabela de Preços/Tipo de Venda !")
	Return(nVlrUnit)
Else
	
	// Retorna Valor Unitario respeitando as regras de Tabela de Preços do Cadastro de Pedidos
	// OBS: Caso _nValor ja esteja preenchido nao altera Valor Unitario
	If _nValor <> nil
		nVlrUnit := _nValor
		nVlrItem := nVlrUnit * aCols[n,nPosQuant]
		nVlrUnBr := nVlrUnit + (aCols[n,nPosValDesc]/aCols[n,nPosQuant])
	Else
		
		If lSaidaR
			nValDig := aCols[N,nPosPrcAtu]
		EndIf
		
		_nValor		:=	P_DELA027(	M->LQ_CODTAB,aCols[n,nPosProduto],aCols[n,nPosQuant],M->LQ_CLIENTE,;
									M->LQ_LOJA,cMoeda,M->LQ_DTLIM,cCond,nValDig)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³O produto pode nao ter preco cadastrado, o que retornaria³
		//³valor zerado na funcao DELA027 - Norbert - 11/07/05      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If _nValor == 0
			nVlrUnit	:=	aCols[N,nPosVrUnit]
		Else
			nVlrUnit	:=	_nValor
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Valor unitario³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nVlrUnBr	:=	nVlrUnit
		nVlrItem	:= NoRound( aCols[n][nPosQuant] * nVlrUnit, nDecimais)
		
		aCols[n,nPosVlrItem]	:= nVlrItem
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calculo do desconto quando se usa o campo LR_DESC,  ³
		//³LR_QUANT, ou o item esta sendo recalculado por outra³
		//³rotina                                              ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If (AllTrim(ReadVar()) $ "M->LR_DESC/M->LR_QUANT") .Or. P_NaPilha("P_DELA020I")

		    nVlrDesc := Round( aCols[n,nPosPercDesc] * aCols[n][nPosVlrItem] / 100, nDecimais )

	    	aCols[n][nPosValDesc] 	:= nVlrDesc
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Calculo do desconto nas demais situacoes³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		Else
		
			nVlrDesc			:= aCols[n,nPosValDesc]
			nVlrPDesc			:= Round( nVlrDesc * 100 / aCols[n][nPosVlrItem], TamSx3("L2_DESC")[2] )

			aCols[n][nPosPercDesc]:= nVlrPDesc
			
		EndIf

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Aplicacao do valor de desconto ao item³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	    aCols[n][nPosVlrItem]	-= nVlrDesc
	    aCols[n][nPosVrUnit]	:= aCols[n][nPosVlrItem] / aCols[n][nPosQuant]  

	Endif

Endif

If N <= Len(aColsDet)	//Item inserido manualmente
	aColsDet[n,nPosPrcTab]   := Round(nVlrUnBr,nDecimais)
Else					//Item inserido via codigo
	lAtuDet := .T.
EndIf

//Desmarca delecao do item
If !aTail(aCols[N]) .And. MaFisFound("IT",N) .And. MaFisRet(N,"IT_DELETED")
	MaFisDel(N,.F.)
EndIf

//Refaz Detalhes
Lj7Detalhe()    

//Refaz calculo de ISS
If MaFisRet(N,"IT_BASEISS") > 0
	MaFisAlt("IT_VALISS",Round(((MaFisRet(N,"IT_BASEISS") * MaFisRet(N,"IT_ALIQISS")) / 100),nDecimais),N)
EndIf

//Se o armazem for nulo, troca por branco
If aColsDet[N][nPosLocal] == NIL
	aColsDet[N][nPosLocal] := Space(02)
EndIf

//Recalcula detalhes para itens inseridos via codigo
If lAtuDet
	aColsDet[n,nPosPrcTab]   := Round(nVlrUnBr,nDecimais)
	Lj7Detalhe()
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Tratamento do TES inteligente³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If (Empty(aCols[N,nPosTpOp]) .And. !Empty(aCols[N,nPosProduto]));
	.Or. AllTrim(aColsDet[n][nPosTES]) == AllTrim(GetMV("MV_TESSAI"));
	.Or. AllTrim(aColsDet[n][nPosTES]) == AllTrim(SB1->B1_TS)
	aCols[N,nPosTpOp] := P_RetTpOp(M->LQ_CLIENTE,M->LQ_LOJA,aCols[N,nPosProduto])
	P_AltTes(.F.)
EndIf

//Preenche o campo preco de tabela no aColsDet, limpo pela Lj7Detalhe()
aColsDet[n,nPosPrcTab]   := Round(nVlrUnBr,nDecimais)

// Executa funcao que Retornara Margem Media e Valor de Despesa do Orcamento
aVlrMM := P_MargemMedia(aCols[n,nPosProduto],aColsDet[n,nPosLocal],aCols[n,nPosQuant],M->LQ_TIPOVND,aCols[n,nPosVlrItem])
aCols[n,nPosMM] 	:= aVlrMM[1]

Lj7T_SubTotal( 2, 0)

// Soma aCols nao deletados
For nX := 1 To naCols
	
	If !aCols[nX,naColsItem]
		
		If MaFisFound("IT",nX) .And. !MaFisRet(nX,"IT_DELETED")
			Lj7T_SubTotal( 2, ( Lj7T_SubTotal(2) + MaFisRet(nX, "IT_TOTAL") ))
		EndIf
		
	EndIf
	
Next nX

// Atualiza Rodapes com Totais
Lj7T_Total(2,Lj7T_SubTot(2) - Lj7T_DescV(2))

//Atualiza o texto do rodape com os dados da condicao de pagamento selecionada
If !Empty(M->LQ_TIPOVND) .And. _lAtuBar .And. !P_NaPilha("P_DELC001G")
	
	P_LjRodape(M->LQ_CONDPG)
	
EndIf

P_CtrlArea(2,_aArea,_aAlias) // RestArea

Return aCols[n][nPosVrUnit]

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³DELA015B  ºAutor  ³Ricardo Mansano     º Data ³  05/05/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºLocacao   ³ Fab.Tradicional  ³Contato ³ mansano@microsiga.com.br       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDescricao ³ Trava troca da Tabela de preço após digitação dos produtos º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºParametros³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºRetorno   ³ lRet = .T. (Continua digitação)                            º±±
±±º          ³        .F. (Impede troca da Tabela de preços.)             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAplicacao ³ ValidUser em LQ_CODTAB                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ 13797 - Dellavia Pneus                                     º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºAnalista  ³Data    ³Bops  ³Manutencao Efetuada                      	  º±±
±±ºMansano   ³09/06/05³      ³Inclusao da validacao de troca de Tabela de º±±
±±º          ³        ³      ³preco de acordo com a Regra no campos    	  º±±
±±º          ³        ³      ³PA6_TRCTAB S=Sim / N=Nao.                	  º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Project Function DELA015B()
Local lRet 		  	:= .T.
Local naCols		:= Len(aCols)
Local nPosProduto 	:= aScan(aHeader,{ |x| AllTrim(x[2]) == "LR_PRODUTO" })
Local _aArea   		:= {}
Local _aAlias  		:= {}

P_CtrlArea(1,@_aArea,@_aAlias,{"PA6"}) // GetArea

// Não Permite alterar Tabela de Preço se aCols estiver preenchida
If ((naCols==1) .and. (AllTrim(aCols[1,nPosProduto])<>"")) .or. (naCols > 1)
	Aviso("Atenção !!!","Cod. da Tabela de Preços não pode ser alterada com Itens lançados !!!",{" << Voltar"},2,"Tabela de Preços !")
	lRet := .F.
Endif

// Verifica se convenio permite Troca de Tabela
// Verifica existencia do Convenio para evitar perda de ponteiro
PA6->(DbSetOrder(1)) //PA6_FILIAL+PA6_COD
If !(PA6->(DbSeek(xFilial("PA6")+M->LQ_CODCON)))
	Aviso("Atenção !!!","Convênio não Localizado !!!",{" << Voltar"},2,"Convenio !")
	lRet := .F.
Else
	If ( PA6->PA6_TRCTAB=="N" ) .and. ( PA6->PA6_TABELA<>M->LQ_CODTAB )
		Aviso("Atenção !!!","Convênio só permite vendas na Tabela de Preço: "+PA6->PA6_TABELA+" !!!",{" << Voltar"},2,"Convenio !")
		lRet := .F.
	Endif
Endif

P_CtrlArea(2,_aArea,_aAlias) // RestArea

Return(lRet)
