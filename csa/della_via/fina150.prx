#INCLUDE "FINA150.CH"
#include "PROTHEUS.ch"
/*/


Ŀ
Funo     Fina150   Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio  Gerao do Arquivo de Envio de Titulos ao Banco            
Ĵ
Sintaxe    Fina150()                                                  
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function Fina150(nPosArotina)

//Ŀ
// Verifica as perguntas selecionadas                           
//
If !(pergunte("AFI150",.T.))
	Return
Endif
//Ŀ
// Variaveis utilizadas para parametros 
// mv_par01		 // Do Bordero 		  
// mv_par02		 // Ate o Bordero 	  
// mv_par03		 // Arq.Config 		  
// mv_par04		 // Arq. Saida    	  
// mv_par05		 // Banco     			  
// mv_par06		 // Agenciao     		  
// mv_par07		 // Conta   			  
// mv_par08		 // Sub-Conta  		  
// mv_par09		 // Cnab 1 / Cnab 2    
// mv_par10		 // Considera Filiais  
// mv_par11		 // De Filial   		  
// mv_par12		 // Ate Filial         
//

PRIVATE cBanco,cAgencia,xConteudo
PRIVATE cPerg      := "AFI150"
PRIVATE nHdlBco    := 0
PRIVATE nHdlSaida  := 0
PRIVATE nSeq       := 0
PRIVATE nSomaValor := 0
PRIVATE aRotina    := { {OemToAnsi(STR0003) , "fA150Par" , 0 , 0 },;  //  "Parametros"   
                        {OemToAnsi(STR0004) , "fA150Gera" , 0 , 0 } }  // "Gerar Arquivo"
PRIVATE nBorderos := 0
PRIVATE xBuffer,nLidos := 0
PRIVATE nTotCnab2 := 0 // Contador de Lay-out nao deletar 
PRIVATE nLinha := 0 // Contador de Linhas nao deletar 

//Ŀ
// Define o cabecalho da tela de baixas                         
//
PRIVATE cCadastro := OemToAnsi(STR0005)  // "Comunicao Bancria-Envio"

DEFAULT nPosArotina := 0

nReg:=Recno( )
If nPosArotina > 0
	dbSelectArea('SE1')
	bBlock := &( "{ |a,b,c,d,e| " + aRotina[ nPosArotina,2 ] + "(a,b,c,d,e) }" )
	Eval( bBlock, Alias(), (Alias())->(Recno()),nPosArotina)
Else
	mBrowse( 6, 1,22,75,"SE1" )
Endif	
dbGoto( nReg )

//Ŀ
// Fecha os Arquivos ASC II                                     
//
FCLOSE(nHdlBco)
FCLOSE(nHdlSaida)

//Ŀ
// Recupera a Integridade dos dados                             
//
dbSelectArea("SE1")
dbSetOrder(1)


/*/


Ŀ
Funo     fA150Gera Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio  Comunicao Bancria - Envio                               
Ĵ
Sintaxe    fA150Gera(cAlias)                                          
Ĵ
 Uso       FinA150                                                    
ٱ


/*/
Function fa150Gera(cAlias)
Processa({|lEnd| fa150Ger(cAlias)})  // Chamada com regua
nBorderos  := 0
nSeq		  := 0
nSomaValor := 0
Return .T.

/*/


Ŀ
Funo     fA150Ger  Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio  Comunicao Bancria - Envio                               
Ĵ
Sintaxe    fA150Ger()                                                 
Ĵ
 Uso       FinA150                                                    
ٱ


/*/
Function fA150Ger(cAlias)
LOCAL nTamArq:=0,lResp:=.t.
LOCAL lHeader:=.F.,lFirst:=.F.,lFirst2:=.F.
LOCAL nTam,nDec,nUltDisco:=0,nGrava:=0,aBordero:={}
LOCAL nSavRecno := recno()
Local lRecicl	:= GETMV("MV_RECICL")
Local cDbf
LOCAL lFIN150_1  := ExistBlock("FIN150_1")
LOCAL lFIN150_2  := ExistBlock("FIN150_2")
LOCAL lFIN150_3  := ExistBlock("FIN150_3")
LOCAL lFINA150   := ExistBlock("FIN150")
Local lFinCnab2  := ExistBlock("FINCNAB2")
LOCAL oDlg,oBmp,nMeter := 1
LOCAL cTexto := "CNAB"
LOCAL nRegEmp := SM0->(RecNo())
LOCAL cFilDe
LOCAL cFilAte
LOCAL cNumBorAnt := CRIAVAR("E1_NUMBOR",.F.)
LOCAL lFirstBord := .T.
LOCAL lBorBlock := .F.
LOCAL lAchouBord := .F.
Local lF150Exc := ExistBlock("F150EXC")
LOCAL lIdCnab := .T.
Local cArqGerado := ""
Local lF150Sum := ExistBlock("F150SUM")
Local lAtuDsk := .F.
Local lCnabEmail := .F.

ProcRegua(SE1->(RecCount()))

//Ŀ
// Verifica se o arquivo est realmente vazio ou se       
// est posicionado em outra filial.                      
//
If EOF() .or. SE1->E1_FILIAL # xFilial("SE1")
	HELP(" " , 1 , "ARQVAZIO")
	Return Nil
Endif

//Ŀ
// Posiciona no Banco indicado                                  
//
cBanco  := mv_par05
cAgencia:= mv_par06
cConta  := mv_par07
cSubCta := mv_par08

dbSelectArea("SA6")
If !(dbSeek(xFilial()+cBanco+cAgencia+cConta))
	Help(" ",1,"FA150BCO")
	Return .F.
Endif

dbSelectArea("SEE")
SEE->( dbSeek(cFilial+cBanco+cAgencia+cConta+cSubCta) )
If !SEE->( found() )
	Help(" ",1,"PAR150")
	Return .F.
Else
	If Val(EE_FAXFIM)-Val(EE_FAXATU) < 100
		Help(" ",1,"FAIXA150")
	Endif
Endif

//Ŀ
// Posiciona no Bordero Informado pelo usuario                  
//
if mv_par10 == 2
	cFilDe := cFilAnt
	cFilAte:= cFilAnt
Else
	cFilDe := mv_par11
	cFilAte:= mv_par12
Endif

lResp:=AbrePar(@cArqGerado)	//Abertura Arquivo ASC II

If !lResp
	Return .F.
Endif

nTotCnab2 := 0
nSeq := 0

If lRecicl
	cDbf := "RECICL" + Substr(cNumEmp,1,2)
	If !File(cDbf+GetDBExtension())
		lRecicl := .F.
	Else
		dbUseArea(.T.,,cDbf,"cTemp",.F.,.F.)
		IndRegua("cTemp",cDbf,"FILIAL+NOSSONUM",,,OemToAnsi(STR0008))  //"Selecionando Registros..."
	EndIf
EndIf		

dbSelectArea("SM0")
dbSeek(cEmpAnt+cFilDe,.T.)
lAchouBord := .F.

While !Eof() .and. M0_CODIGO == cEmpAnt .and. M0_CODFIL <= cFilAte

	cFilAnt := M0_CODFIL

	//Ŀ
	// Inicia a leitura do arquivo de Titulos                       
	//
	dbSelectArea("SE1")
	SE1->( dbSetOrder(5) )
	SE1->( dbSeek(xFilial()+mv_par01,.T.))
	While !SE1->( Eof()) .and. E1_NUMBOR >= mv_par01 .AND. E1_NUMBOR <= mv_par02 .and. xFilial()==E1_FILIAL

		lAchouBord := .T.
		IncProc()

		IF Empty(SE1->E1_NUMBOR) .or. (SE1->E1_NUMBOR == cNumBorAnt .and. lBorBlock)
			SE1->( dbSkip() )
			Loop
		EndIF

		//Ŀ
		// Verificacao do usuario se o bordero deve ser considerado     
		//
		If lF150Exc
			If !(ExecBlock("F150EXC",.F.,.F.))
				SE1->( dbSkip() )
				Loop
			Endif
		Endif

		//Ŀ
		// Verifica se o portador do bordero  o mesmo dos parametros   
		//
		If SE1->E1_NUMBOR != cNumBorAnt .or. lFirstBord
			lFirstBord := .F.
			dbSelectArea("SEA")
			If dbSeek(xFilial()+SE1->E1_NUMBOR)
				While SEA->EA_NUMBOR == SE1->E1_NUMBOR .and. SEA->EA_FILIAL == xFilial() .and. !Eof()
					If SEA->EA_CART == "R"
						cNumBorAnt := SE1->E1_NUMBOR
						lBorBlock := .F.
						If cBanco+cAgencia+cConta != SEA->(EA_PORTADO+EA_AGEDEP+EA_NUMCON)
							//Ŀ
							// Bordero pertence a outro Bco/Age/Cta 
							//
							Help(" ",1,"NOBCOBORD",,cNumBorAnt,4,1) 
							lBorBlock := .T.
						Endif
						Exit
					Else
						//Ŀ
						// Bordero pertence a outra Carteira (Pagar) 
						//
						lBorBlock := .T.
						SEA->(dbSkip())
						Loop
					Endif
				Enddo
			Else
				//Ŀ
				// Bordero no foi achado no SEA        
				//
				Help(" ",1,"BORNOXADO",,SE1->E1_NUMBOR,4,1)
				lBorBlock := .T.
			Endif
		Endif
		dbSelectArea("SE1")
		If lBorBlock
			SE1->( dbSkip() )
			Loop
		Endif

		IF SE1->E1_TIPO $ MVRECANT+"/"+MVPROVIS
			SE1->( dbSkip() )
			Loop
		EndIF

		//Ŀ
	   // Posiciona no cliente                                         
		//
		dbSelectArea("SA1")
		dbSeek(cFilial+SE1->E1_CLIENTE+SE1->E1_LOJA)
		lCnabEmail := If(FieldPos("A1_BLEMAIL") > 0, A1_BLEMAIL == "1", .F.)
		If lFin150_1
			Execblock("FIN150_1",.F.,.F.)
		Endif
		//Ŀ
	   // Posiciona no Contrato bancario                               
		//
		dbSelectArea("SE9")
		dbSetOrder(1)
		dbSeek(xFilial("SE9")+SE1->(E1_CONTRAT+E1_PORTADO+E1_AGEDEP))
		
		dbSelectArea("SE1")

		If lRecicl
			dbSelectArea("cTemp")
			dbSeek(xFilial("SE1")+SE1->E1_NUMBCO)
			RecLock("cTemp",.t.)
			dbDelete()
			MsUnLock()
		EndIf
	
		nSeq++
		If lF150Sum
			nSomaValor += ExecBlock("F150SUM",.F.,.F.)
		Else
			nSomaValor += SE1->E1_SALDO
   	Endif

		If ( MV_PAR09 == 1 )
			//Ŀ
			// Le Arquivo de Parametrizacao                                 
			//
			nLidos:=0
			FSEEK(nHdlBco,0,0)
			nTamArq:=FSEEK(nHdlBco,0,2)
			FSEEK(nHdlBco,0,0)
			lIdCnab := .T.
	
			While nLidos <= nTamArq

				//Ŀ
				// Verifica o tipo qual registro foi lido                       
				//
				xBuffer:=Space(85)
				FREAD(nHdlBco,@xBuffer,85)
	
				Do Case
					Case SubStr(xBuffer,1,1) == CHR(1)
						IF lHeader
							nLidos+=85
							Loop
						EndIF
					Case SubStr(xBuffer,1,1) == CHR(2)
						lFirst2 := .F. //Controle do detalhe tipo 5	
						IF !lFirst
							lFirst := .T.
							FWRITE(nHdlSaida,CHR(13)+CHR(10))
							If lFina150
								Execblock("FIN150",.F.,.F.)
							Endif
						EndIF
					Case SubStr(xBuffer,1,1) == CHR(4) .and.  lCnabEmail
						IF !lFirst2
							nSeq++
							lFirst2 := .T.
							FWRITE(nHdlSaida,CHR(13)+CHR(10))
						Endif
					Case SubStr(xBuffer,1,1) == CHR(3)
						nLidos+=85
						Loop
					Otherwise
						nLidos+=85
						Loop
				EndCase

				nTam := 1+(Val(SubStr(xBuffer,20,3))-Val(SubStr(xBuffer,17,3)))
				nDec := Val(SubStr(xBuffer,23,1))
				cConteudo:= SubStr(xBuffer,24,60)
				nGrava := fA150Grava(nTam,nDec,cConteudo,@aBordero,,lFinCnab2,@lIdCnab)
				If nGrava != 1
					Exit
				Endif
				dbSelectArea("SE1")
				nLidos+=85
			EndDO
			If nGrava == 3
				Exit
			Endif
		Else
			nGrava := fA150Grava(,,,@aBordero,,lFinCnab2,@lIdCnab)
		EndIf
	   If nGrava == 1
			lAtuDsk := .T.
			If ( MV_PAR09 == 1 )
				lIdCnab := .T.	// Para obter novo identificador do registro CNAB na rotina 
									// FA150GRAVA
	   		fWrite(nHdlSaida,CHR(13)+CHR(10))
				IF !lHeader
					lHeader := .T.
				EndIF
			Endif
			dbSelectArea("SEA")
			If (dbSeek(xFilial()+SE1->E1_NUMBOR+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO))
				Reclock("SEA")
				SEA -> EA_TRANSF := "S"
				MsUnlock()
			Endif
			If lRecicl
				dbSelectArea("SE1")
				RecLock("SE1")
				Replace E1_OCORREN With "01"
				MsUnLock()
			EndIf	
   	Endif

		If lFin150_2
			nSeq++
			If !(Execblock("FIN150_2",.f.,.f.))		// No incrementou
				nSeq--
			Endif
		Endif
		dbSelectArea("SE1")
		SE1->( dbSkip())
	Enddo
	If Empty(xFilial("SE1"))
		Exit
	Endif
	dbSelectArea("SM0")
	dbSkip()
EndDO

SM0->(dbgoto(nRegEmp))
cFilAnt := SM0->M0_CODFIL

If !lAchouBord
	Help(" ",1,"BORD150")
	Return .F.
EndIF

If ( mv_par09 == 1 )
	//Ŀ
	// Monta Registro Trailler                              		  
	//
	nSeq++
	nLidos:=0
	FSEEK(nHdlBco,0,0)
	nTamArq:=FSEEK(nHdlBco,0,2)
	FSEEK(nHdlBco,0,0)
	While nLidos <= nTamArq

		//Ŀ
		// Tipo qual registro foi lido                                  
		//
		xBuffer:=Space(85)
		FREAD(nHdlBco,@xBuffer,85)

		IF SubStr(xBuffer,1,1) == CHR(3)
			nTam := 1+(Val(SubStr(xBuffer,20,3))-Val(SubStr(xBuffer,17,3)))
			nDec := Val(SubStr(xBuffer,23,1))
			cConteudo:= SubStr(xBuffer,24,60)
			nGrava:=fA150Grava( nTam,nDec,cConteudo,@aBordero,.T.,lFinCnab2,.F.)
		Endif
		nLidos+=85
	End
Else
	RodaCnab2(nHdlSaida,MV_PAR03)
EndIf
//Ŀ
// Atualiza Numero do ultimo Disco                              
//
dbSelectArea("SEE")
IF !Eof() .and. nGrava != 3 .and. lAtuDsk
	Reclock("SEE")
	nUltDisco:=VAL(EE_ULTDSK)+1
   Replace EE_ULTDSK With StrZero(nUltDisco,TamSx3("EE_ULTDSK")[1])
   MsUnlock()
EndIF
If ( MV_PAR09 == 1 )
	FWRITE(nHdlSaida,CHR(13)+CHR(10))
EndIf

dbSelectArea( cAlias )
dbGoTo( nSavRecno )

//Ŀ
// Verifica se existe logotipo de um banco qualquer             
// para ser utilizado em qualquer banco                         
//
If File("BANCO.BMP")
	If MsgYesNo(Oemtoansi("Foi detectado a gerao de um arquivo padro "+Substr(SA6->A6_NREDUZ,1,8)+ " , Confirma a Gerao"))
		DEFINE DIALOG oDlg FROM 100,100 TO 365,550 TITLE cTexto PIXEL
		@0,0 BITMAP oBmp FILENAME "BANCO.BMP" PIXEL OF oDlg SIZE 225,125 NOBORDER
		@125,0 METER oMeter VAR nMeter TOTAL 100000 SIZE 224,8 OF oDlg PIXEL
		DEFINE SBUTTON FROM 2000,3000 TYPE 2  ENABLE OF oDlg
		oDlg:bStart := {|| CursorWait(), Fa150Process(oDlg,oMeter) }
		ACTIVATE DIALOG oDlg CENTERED
		RELEASE OBJECTS oBmp
	Endif		
Endif

//Ŀ
// Fecha o arquivo gerado.                                      
//
FCLOSE(nHdlSaida)
If nGrava == 3 // Abandonou a rotina, quando o bordero ja foi enviado.
	// Apaga o arquivo de saida, para nao caracterizar um erro no programa
	// a geracao incompleta do arquivo quando o usuario cancelar.
	fErase(cArqGerado)
Endif

If lRecicl 
	dbSelectArea("cTemp")
	Pack
	DbCloseArea()
EndIF	
If lFin150_3
	Execblock("FIN150_3",.F.,.F.)
Endif	

Return(.T.)

/*/


Ŀ
Funo    AbrePar    Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio Abre arquivo de Parametros                                  
Ĵ
Sintaxe   AbrePar()                                                   
Ĵ
 Uso      FinA150                                                     
ٱ


/*/
Function AbrePar(cArqGerado)
LOCAL cArqEnt:=mv_par03,cArqSaida

IF AT(".",mv_par04)>0
	cArqSaida:=SubStr(TRIM(mv_par04),1,AT(".",mv_par04)-1)+"."+TRIM(SEE->EE_EXTEN)
Else
	cArqSaida:=TRIM(mv_par04)+"."+TRIM(SEE->EE_EXTEN)
EndIF

cArqGerado := cArqSaida

IF !FILE(cArqEnt)
	Help(" ",1,"NOARQPAR")
	Return .F.
Else
	If ( MV_PAR09 == 1 )
		nHdlBco:=FOPEN(cArqEnt,0+64)
	Endif
EndIF

//Ŀ
// Cria Arquivo Saida                                       
//
If ( MV_PAR09 == 1 )
	nHdlSaida:=MSFCREATE(cArqSaida,0)
Else
	nHdlSaida:=HeadCnab2(cArqSaida,MV_PAR03)
Endif
Return .T.

/*/


Ŀ
Funo    fA150Grava Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio Rotina de Geracao do Arquivo de Remessa de Comunicacao      
          Bancaria                                                    
Ĵ
Sintaxe   ExpL1:=fa150Grava(ExpN1,ExpN2,ExpC1)                        
Ĵ
 Uso       FinA150                                                    
ٱ


/*/
STATIC Function fA150Grava( nTam,nDec,cConteudo,aBordero,lTrailler,lFinCnab2,lIdCnab)
Local nRetorno := 1
Local nX       := 1
Local oDlg, oRad, nTecla
Local cIdCnab
Local aGetArea := GetArea()
Local aOrdSe1  := {}

DEFAULT lIdCnab := .F.

lTrailler := IIF( lTrailler==NIL, .F., lTrailler ) // Para imprimir o trailler
                                                   // caso se deseje abandonar
                                                   // a gerao do arquivo
                                                   // de envio pela metade

lFinCnab2 := Iif( lFinCnab2 == Nil, .F., lFinCnab2 )

//Ŀ
// O retorno podera' ser :                                  
// 1 - Grava Ok                                             
// 2 - Ignora bordero                                       
// 3 - Abandona rotina                                      
//
//Ŀ
// Verifica se titulo ja' foi enviado                       
//
dbSelectArea("SEA")
If	(!SE1->( Eof()) .and. SE1->E1_NUMBOR >= mv_par01 .AND. SE1->E1_NUMBOR <= mv_par02 .and. xFilial()==SE1->E1_FILIAL) .And.;
	(dbSeek(xFilial()+SE1->E1_NUMBOR+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO))
	If SEA->EA_TRANSF == "S"
		nX := ASCAN(aBordero,SubStr(SE1->E1_NUMBOR,1,6))
		If nX == 0
			nOpc := 0
			DEFINE MSDIALOG oDlg FROM  35,   37 TO 188,383 TITLE OemToAnsi(STR0009) PIXEL  // "Bordero Existente"
			@ 11, 7 SAY OemToAnsi(STR0010) 			SIZE 58, 7 OF oDlg PIXEL  // "O border nmero:"
			@ 11, 68 MSGET SE1->E1_NUMBOR When .F. SIZE 37, 10 OF oDlg PIXEL
			@ 24, 7 SAY OemToAnsi(STR0011) 			SIZE 82, 7 OF oDlg PIXEL  // "j foi enviado ao banco."
			@ 37, 6 TO 69, 120 LABEL OemToAnsi(STR0012) OF oDlg  PIXEL  //"Para prosseguir escolha uma das opes"
			@ 45, 11 RADIO oRad VAR nTecla 3D 		SIZE 65, 11 PROMPT OemToAnsi(STR0013),OemToAnsi(STR0014) OF oDlg PIXEL  // "Gera com esse border"###"Ignora esse border"

			DEFINE SBUTTON FROM 11, 140 TYPE 1 ENABLE OF oDlg Action (nOpc:=1,oDlg:End())
			DEFINE SBUTTON FROM 24, 140 TYPE 2 ENABLE OF oDlg Action (nopc:=0,oDlg:End())
			ACTIVATE MSDIALOG oDlg Centered
			If nOpc == 1
				If nTecla == 1
					nRetorno := 1
					nBorderos++
				Else
					nRetorno := 2
				EndIf
			Else
				nRetorno := 3
			EndIf				
		Else
			nRetorno := Int(Val(SubStr(aBordero[nX],7,1)))
		EndIf
	EndIf
EndIf
If nRetorno == 1 .or. ( lTrailler .and. nBorderos > 0 )
	If ( MV_PAR09 == 1 )
		If !lTrailler .and. lIdCnab .And. Empty(SE1->E1_IDCNAB) // So gera outro identificador, caso o titulo
															 // ainda nao o tenha, pois pode ser um re-envio do arquivo
			// Gera identificador do registro CNAB no titulo enviado
			cIdCnab := GetSxENum("SE1", "E1_IDCNAB","E1_IDCNAB"+cEmpAnt)
			dbSelectArea("SE1")
			aOrdSE1 := SE1->(GetArea())
			dbSetOrder(16)
			While SE1->(dbSeek(xFilial("SE1")+cIdCnab))
				If ( __lSx8 )
					ConfirmSX8()
				EndIf
				cIdCnab := GetSxENum("SE1", "E1_IDCNAB","E1_IDCNAB"+cEmpAnt)
			EndDo
			SE1->(RestArea(aOrdSE1))
			Reclock("SE1")
			SE1->E1_IDCNAB := cIdCnab
			MsUnlock()
			ConfirmSx8()
			lIdCnab := .F. // Gera o identificacao do registro CNAB apenas uma vez no
								// titulo enviado
		Endif
		//Ŀ
		// Analisa conteudo                                         
		//
		IF Empty(cConteudo)
			cCampo:=Space(nTam)
		Else
			lConteudo := fa150Orig( cConteudo )
			IF !lConteudo
				RestArea(aGetArea)
				Return nRetorno
			Else
				IF ValType(xConteudo)="D"
					cCampo := GravaData(xConteudo,.F.)
				Elseif ValType(xConteudo)="N"
					cCampo:=Substr(Strzero(xConteudo,nTam,nDec),1,nTam)
				Else
					cCampo:=Substr(xConteudo,1,nTam)
				EndIf
			EndIf
		EndIf
		If Len(cCampo) < nTam  //Preenche campo a ser gravado, caso menor
			cCampo:=cCampo+Space(nTam-Len(cCampo))
		EndIf
		Fwrite( nHdlSaida,cCampo,nTam )
	Else
		nTotCnab2++
		DetCnab2(nHdlSaida,MV_PAR03,lIdCnab,"SE1")
		lIdCnab := .T.	// Para obter novo identificador do registro CNAB na rotina 
							// DetCnab2
		If lFinCnab2
			nSeq := Execblock("FINCNAB2",.F.,.F.,{nHdlSaida,nSeq,nTotCnab2})
		EndIf
	EndIf
EndIf
If nX == 0
	Aadd(aBordero,Substr(SE1->E1_NUMBOR,1,6)+Str(nRetorno,1))
EndIf
RestArea(aGetArea)
Return nRetorno

/*/


Ŀ
Funo    fA150Par   Autor  Wagner Xavier          Data  26/05/92 
Ĵ
Descrio Aciona parametros do Programa                               
Ĵ
Sintaxe    		                                                     
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function fA150Par()
Pergunte(cPerg)
Return .T.

/*/


Ŀ
Funo     NossoNum  Autor  Paulo Boschetti        Data  05/11/93 
Ĵ
Descrio  Retorna digito de controle                                 
Ĵ
Sintaxe    		                                                     
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function NossoNum( )

Local cNumero

cNumero:= StrZero(Val(SEE->EE_FAXATU),Len(SEE->EE_FAXATU))

If Empty(SE1->E1_NUMBCO)
	RecLock("SE1",.f.)
	Replace SE1->E1_NUMBCO With cNumero
	SE1->( MsUnlock( ) )
	dbSelectArea("SEE")
	RecLock("SEE",.f.)
	Replace EE_FAXATU With Soma1(cNumero,Len(SEE->EE_FAXATU))
	SEE->( MsUnlock() )
	dbSelectArea("SE1")
EndIf	
Return(SE1->E1_NUMBCO)
/*/

Ŀ
Funo     Fa150Chav Autor  Paulo Boschetti        Data  10/11/93 
Ĵ
Descrio                                                             
Ĵ
Sintaxe    Fa150Num()                                                 
Ĵ
 Uso       FINA130                                                    
ٱ


/*/
Function Fa150Chav()
LOCAL lRetorna := .T.
If !Empty(m->ee_codigo) .And. !Empty(m->ee_agencia) .And. !Empty(m->ee_conta) .And. !Empty(m->ee_subcta)
	dbSelectArea("SEE")
	SEE->( dbSeek(cFilial+m->ee_codigo+m->ee_agencia+m->ee_conta+m->ee_subcta) )
	If SEE->( Found() )
		Help(" ",1,"FA150NUM")
		lRetorna := .F.
	EndIf
EndIf
Return lRetorna


/*/

Ŀ
Funo    fa150Orig  Autor  Wagner Xavier          Data  10/11/92 
Ĵ
Descrio Verifica se expressao e' valida para Remessa CNAB.          
Ĵ
 Uso      Fina150                                                     
ٱ


/*/
Function fa150Orig( cForm )
Local bBlock:=ErrorBlock()
Private lRet := .T.

BEGIN SEQUENCE
	xConteudo := &cForm
END SEQUENCE
ErrorBlock(bBlock)
Return lRet


/*/


Ŀ
Funo     SomaValor Autor  Vinicius Barreira      Data  09/01/95 
Ĵ
Descrio  Retorna o valor total dos titulos remetidos                
Ĵ
Sintaxe    SomaValor()                                                
Ĵ
 Uso       Generico                                                   
ٱ


/*/
Function SomaValor()
Return nSomaValor * 100

Static Function Fa150Process(oDlg,oMeter)
Local ni
oMeter:nTotal := 1000
oMeter:Set(0)
For ni:= 1 to 1000
	oMeter:Set(ni)
	SysRefresh()
Next
oDlg:End()
Return Nil
