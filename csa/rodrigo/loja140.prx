#INCLUDE "LOJA140.CH"
#Include "FIVEWIN.Ch"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ Loja140  ³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 02.01.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Exclus„o de Notas Fiscais                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SigaLoja                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ ATUALIZACOES SOFRIDAS DESDE A CONSTRUCAO INICIAL.                     ³±±
±±ÃÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  DATA  ³ BOPS ³Program.³ MOTIVO DA ALTERACAO                          ³±±
±±ÃÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³23/07/02³016840³Edilson ³Removida a declaracao Criavar(), onde a mesma ³±±
±±³        ³      ³        ³gerava inconsistencia na geracao do numero do ³±±
±±³        ³      ³        ³orcamento.                                    ³±±
±±³25/09/02³060215³Edilson ³Acrescentada a verificacao da configuracao do ³±±
±±³        ³      ³        ³caixa, solicitando a senha do supervisor caso ³±±
±±³        ³      ³        ³o caixa tenha sido configurado para o mesmo.  ³±±
±±³14/11/02³060812³Edilson ³Na exclusao da NF gerada foi agregado o valor ³±±
±±³        ³      ³        ³dos campos L1_VLRTOT e L1_VLRLIQ para os      ³±±
±±³        ³      ³        ³campos L1_VALBRUT e L1_VALMERC onde os mesmo  ³±±
±±³        ³      ³        ³tem o valor alterado no instante da finaliza- ³±±
±±³        ³      ³        ³cao do orcamento.                             ³±±
±±³21/02/03³062545³Edilson ³Inplementacao da verificacao de usuarios      ³±±
±±³        ³      ³        ³fiscais e nao-fiscais na exclusão da NF.      ³±±
±±³29/04/03³MELHOR³Fernando³Inplementacao da exclusao de reserva e os     ³±±
±±³        ³      ³        ³orcamentos nele amarrados.                    ³±±
±±³03/10/03³066902³Ivan    ³Ajuste na atualiz. do SF3 verificar se NF de  ³±±
±±³        ³      ³        ³Saida ou NF de Entrada. Atualiz apenas NFSaida³±±
±±³04/07/05³081261³Machima ³Integracao da rotina com o SIGACRD. Cancelar o³±±
±±³        ³      ³        ³contrato na exclusao da NF ou orcamento       ³±±
±±³03/08/05³083747³Geronimo³Na Exclusao restaura o L1_BLCRED  com "" para ³±±
±±³        ³      ³        ³permitir uma nova analise de estoque/credito  ³±±
±±ÀÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function LOJA140

Local cLabelOpcao   := STR0003  //"Exclus„o NF/Orc."
Local cLock 		:= Substr(cUsuario,7,15)+cEstacao
Local aCores 		:= {{'!Empty(L1_DOC).AND.!Empty(L1_SERIE).AND.L1_STATUS<>"D"',																						"BR_VERMELHO"},;
{'Empty(L1_DOC).AND. Empty(L1_RESERVA).AND.dDataBase<=L1_DTLIM.AND.L1_STATUS<>"D"'	,															"BR_VERDE"},;
{'Empty(L1_DOC).AND.!Empty(L1_RESERVA).AND.Empty(L1_DOCPED).AND.L1_STATUS<>"D"'	,							"BR_AMARELO"},;
{'Empty(L1_DOC).AND. Empty(L1_RESERVA).AND.dDataBase>L1_DTLIM.AND.L1_STATUS<>"D"'	,															"BR_PRETO"},;
{'Empty(L1_DOC).AND. L1_TIPO=="P".AND.!Empty(L1_DOCPED).AND.L1_STATUS<>"D"',									"BR_AZUL"},;
{'L1_STATUS="D"',																																"BR_MARROM"}}

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Faz o controle via LockByName para evitar que um usuário acesse       ³
//³ 2 vezes uma rotina que use os periféricos de automação, evitando assim³
//³ a concorrência dos mesmos.                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If SetMDIChild( 0 ) .AND. !LockByName( cLock )
	Return Nil
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis 										     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Private cCadastro    := STR0001	  // Exclus„o N.F./Orcam.
Private aTefDados    := {}
Private lVendaRapida := .F.
Private aRotina      := {}
If cPaisLoc <> "BRA"
	Private lNFManual  := .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Array contendo as Rotinas a executar do programa 	 ³
//³ ----------- Elementos contidos por dimensao ------------	 ³
//³ 1. Nome a aparecer no cabecalho 							 ³
//³ 2. Nome da Rotina associada									 ³
//³ 3. Usado pela rotina										 ³
//³ 4. Tipo de Transa‡„o a ser efetuada							 ³
//³	 1 - Pesquisa e Posiciona em um Banco de Dados				 ³
//³	 2 - Exclui a nota fiscal/orcamento/pedido                   ³
//³	 3 - Anula nota fiscal (localizacoes)                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Pesquisa / Exclus„o NF/Orc.                                          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "BRA"
	aRotina := {{ STR0002 ,"AxPesqui"   , 0 , 1},;
	{ STR0003 ,"lj140Exc"   , 0 , 5} }
Else
	If cPaisLoc == "SAL"
		cCadastro   := OemToAnsi(STR0061)  //"Exclus„o Doc.Saida/Orcam."
		cLabelOpcao := STR0062   //"Exclus„o Doc/Orc."
	ElseIf cPaisLoc == "GUA"
		cLabelOpcao := STR0065  //"Cancelar NF(NCC)"
	EndIf
	aRotina := {{ STR0002      ,"AxPesqui"    , 0 , 1},;  //Pesquisa
	{ cLabelOpcao ,"lj140Exc"   , 0 , 5},;  //"Exclus„o NF/Orc."
	{ STR0031     ,"lj140Anul"  , 0 , 5}}   //"Cancelar"
EndIf

AAdd(aRotina,{"Legenda","Lj140Leg",0,8})

dbSelectArea("SL1")
dbSetOrder(1)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Endere‡a a fun‡„o de BROWSE									 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
mBrowse(,,,,"SL1",,,,,, aCores )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Recupera a Integridade dos dados						     ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
msUnlockAll( )
Return( Nil )

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Exc ³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 02.01.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de notas fiscais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140Exc(ExpC1,ExpN1,ExpN2)                                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140Exc(cAlias,nReg,nOpcx)
Local nCnt:=0
Local lVendido:=.F.
Local nOpca
Local cNumPdv := Space(10)
Local cNumPdvAtu := Space(4)
Local oDlg
Local oCheckNFMan
Local lLj140CanT := (ExistTemplate("LJ140CAN"))
Local lLj140Can  := (ExistBlock("LJ140CAN"))
Local nUsado:=0
Local cRet       :=space(10)
Local cNumCupFis := Space(TamSX3("L1_NUMCFIS")[1])
Local nDecs      := MsDecimais(SL1->L1_MOEDA)
Local nPosVlrTot := 0,nPosDescPro:=0,nPosVlrUni:=0,nPosQuant:=0
Local nY,nImpostos:=0,nImpsInc:=0,aPropImp:={}
Local nRet   := 0
Local cSerie := AllTrim(ljGetStation("SERIE"))
Local lTitCarteira 		:= .T.				// Indica se o titulo esta em carteira (.T.) ou com algum portador (.F.)
Local cDescMot := ""
Local xRet
Local nAbatimentos      := 0

Private aHeader[0],aCampos:={}
If cPaisLoc <> "BRA"
	Private lAnulaSF3  := AtIsRotina("LJ140ANUL")
	If cPaisLoc == "GUA"
		Private cNumNFDev  := CriaVar("L1_DOC") //Numero da Nota de Credito a ser gerada
		Private cSerieDev  := AllTrim(GetMV("MV_LJNFTRO"))
		Private cMotivo    := Space(6)
		Private lGeraNCC   := !lAnulaSF3
		Private cTipo      := "D"
		lAnulaSF3  := .T.
	EndIf
EndIf

If FunName()='LOJA220'
	
	If !lFiscal
		Return .T.
	EndIf
	nRet := IFStatus( nHdlECF, '5', @cRet )				// Verifica se o cupom está aberto
	If nRet == 7
		MsgInfo(OemToAnsi(STR0024)) //'Cupom nao foi finalizado'
		Return .T.
	EndIf
	nRet := IFPegCupom( nHdlECF, @cNumCupFis)
	nRet := IFPegPDV( nHdlECF, @cNumPdvAtu )
	if Empty(cNumPdvatu).OR.empty(cNumCupFis)
		MsgInfo(OemToAnsi(STR0025)) //'Erro de comunicacao com a impressora fiscal'
		Return .T.
	EndIf
	cNumCupFis := Repl('0',TamSX3("L1_NUMCFIS")[1]-Len(Alltrim(cNumCupFis))) + Alltrim(cNumCupFis)
	cSerie     := cSerie+Space(TamSX3("L1_SERIE")[1]-Len(cSerie))
	dbselectArea("SL1")
	dbsetorder(2)
	if !MsSeek(xFilial("SL1")+cSerie+cNumCupFis+cNumPdvAtu)
		MsgInfo(OemToAnsi(STR0026)+cNumCupFis) //Nao foi encontrado registro do cupom :
		Return .T.
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ A ocorrencia 42 (ACS), verifica se o usu rio poder  ou n„o  ³
//³ efetuar a exclusÆo da nota fiscal. 						    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ChkPsw( 42 )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Confere a configuracao do caixa, verificando se o mesmo     ³
//³ possui permissao para o cancelamento do cupom.              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !LJProFile(8)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Restaura a integridade da janela ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea(cAlias)
	Return .F.
ENDIF
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se Caixa esta Aberto  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !ljCxAberto()
	Return( Nil )
EndIf

nOpcA 	 := 0
lContinua := .F.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se a data da nota ‚ menor que a database, caso seja devera³
//³ fazer a devolu‡Æo da nota 										   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If !Empty(SL1->L1_DOC)
	SF2->( dbSetOrder(1) )
	SF2->( MsSeek( xFilial("SF2") + SL1->L1_DOC + SL1->L1_SERIE ) )
	
	If (SF2->F2_EMISSAO) < (dDataBase)
		Help(" ",1,"LJ140EXC")
		Return( Nil )
	EndIf
ElseIf cPaisLoc == "GUA" .AND. lGeraNCC
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|    "Aten‡„o"###"Nao e permitido gerar Nota de Credito para cancelamento de orcamento."###"OK"           |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Aviso(STR0006,STR0071,{STR0048})
	Return NIL
EndIf

If Empty(SL1->L1_NUM)	// Verifica se Nao ‚ Devolucao
	Help(" ",1,"NFDEVOL")
	Return( Nil )
ELse
	If !Empty(SL1->L1_DOC)
		If Month(SF2->F2_EMISSAO) <> Month(dDataBase) .OR. Year(SF2->F2_EMISSAO) <> Year(dDataBase)
			Help(" ",1,"FORA_MES")
			Return( Nil )
		Endif
	Endif
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se o caixa pode excluir a Nota Fiscal ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(SL1->L1_DOC)
	If !lLj140Can		// Ponto de Entrada para n„o considerar tais condi‡oes
		If __cUserID <> "000000" 		 //Se Administrador, pode excluir a nota
			If xNumCaixa() <> SL1->L1_OPERADO
				If !ChkPsw(42)
					Help(" ",1,"OUTROCAIXA")
					Return( Nil )
				EndIf
			EndIf
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Tratamento para pedidos (reserva)                                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Empty( SL1->L1_DOCPED ) .AND. !Empty( SL1->L1_FILRES )
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//|  "Este orcamento não poderá ser excluido porque se trata de um orçamento com reserva."                  |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	MsgStop( STR0037 , STR0006 )
	Return NIL
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Ponto de Entrada para uso de Template          |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj140CanT	// Executa um ponto de entrada para outras verifica‡”es
	lLj140CanT := ExecTemplate("LJ140CAN",.F.,.F.)
	if !lLj140CanT
		Help(" ",1,"NO140CANC")
		Return( Nil )
	Endif
Endif
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Ponto de Entrada      							|
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLj140Can	// Executa um ponto de entrada para outras verifica‡”es
	lLj140Can := ExecBlock("LJ140CAN",.F.,.F.)
	if !lLj140Can
		Help(" ",1,"NO140CANC")
		Return( Nil )
	Endif
Endif


SL2->(dbSetOrder(1))
SL2->(MsSeek(xFilial("SL2")+SL1->L1_NUM))
While  !SL2->(Eof()) .AND. xFilial("SL2") + SL2->L2_NUM == xFilial("SL1") + SL1->L1_NUM
	If SL2->L2_VENDIDO == "S"
		lVendido:=.T.
	EndIf
	nCnt++
	If SL2->L2_STATUS == "D"
		Help(" ","1","JATROCADO")
		Return( Nil )
	EndIf
	SL2->(dbSkip())
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Avisa o usuario sobre a geracao ou nao da Nota de Credito - Loc. Guatemala  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If cPaisLoc == "GUA" .AND. lVendido
	If lGeraNCC
		If !MsgYesNo(STR0066)  //"Esta operacao de cancelamento vai gerar uma Nota de Credito ao Cliente. Confirma?"
			Return NIL
		EndIf
	Else
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     "Esta operacao de cancelamento nao gera Nota de Credito."                                           |
		//|     "Deve ser utilizada quando o pagamento da Nota Fiscal nao foi efetuado ou "                         |
		//|     "na ocorrencia de erro na impressao do documento. Confirma?"                                        |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !MsgYesNo(STR0067+chr(13)+STR0068+chr(13)+STR0069)
			Return NIL
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se foi cupom fiscal e se ‚ a impressora que o emitiu ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !Empty(SL1->L1_PDV) .AND. lVendido .AND. FunName()<>'LOJA220'
	cNumPdv := SL1->L1_PDV
	If !lFiscal
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|    Esta venda s¢ poder  ser exclu¡da pela impressora fiscal                                             |
		//|    que efetuou a mesma. Numero da impressora que registrou a venda:                                     |
		//|    Aten‡„o                                                                                              |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		MsgStop(Oemtoansi(STR0004 + STR0005+cNumPdv),Oemtoansi(STR0006))
		Return( Nil )
	Else
		
		cNumPdvAtu := Space(10)
		iRet := IFPegPDV( nHdlECF, @cNumPdvAtu )
		
		
		If AllTrim(cNumPdv) <> AllTrim(cNumPdvAtu)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|   Esta venda foi efetuada por outro caixa                                                               |
			//|   O PDV que efetuou a venda foi:                                                                        |
			//|   Aten‡„o                                                                                               |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			MsgStop(Oemtoansi(STR0007 + STR0008 + cNumPdv),Oemtoansi(STR0006))
			Return( Nil )
			
		EndIf
		
	EndIf
	
	If lFiscal
		
		cNumAnt := Space(10)
		If cPaisLoc == "ARG"
			Aviso(STR0047,STR0049,{OemToAnsi(STR0048)})  //"Atencao"###"Para cancelar um cupom fiscal gerar uma Nota de Credito."###"OK"
			Return(Nil)
		Else
			iRet := IFPegCupom( nHdlECF, @cNumAnt, "T" )
			cNumAnt := StrZero(Val(cNumAnt),TamSX3("L1_NUMCFIS")[1])
		EndIf
		
		If SL1->L1_NUMCFIS  == cNumAnt
			lOk := .T.
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|   Por se tratar de impressora fiscal, s¢ poder  ser excluido o £ltimo                                   |
			//|   Cupom vendido. (Vide convˆnio ICMS - 156/96)                                                          |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			
			MsgStop(Oemtoansi(STR0009)+Oemtoansi(STR0010))
			Return Nil
		EndIf
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se os titulos estao em carteira para exclusão. Se algum titulo estiver³
//³ com algum portador nao podera ser efetuada a exclusao do titulo.               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
lTitCarteira := .T.
If !Empty(SL1->L1_DOC)
	SE1->( dbSetOrder(2) )	// E1_FILIAL+E1_CLIENTE+E1_LOJA+E1_PREFIXO+E1_NUM+E1_PARCELA+E1_TIPO
	SE1->( MsSeek(xFilial("SE1")+SL1->L1_CLIENTE+SL1->L1_LOJA+SL1->L1_SERIE+SL1->L1_DOC ))
	While SE1->(!Eof()) .AND. ;
		SE1->E1_FILIAL+SE1->E1_CLIENTE+SE1->E1_LOJA+SE1->E1_PREFIXO+SE1->E1_NUM == xFilial("SE1")+SL1->L1_CLIENTE+SL1->L1_LOJA+SL1->L1_SERIE+SL1->L1_DOC
		
		If !Empty(SE1->E1_PORTADO) .AND. !IsCaixaLoja(SE1->E1_PORTADO)
			lTitCarteira := .F.
		Endif
		SE1->(dbSkip())
	End
Endif
If !lTitCarteira
	Help(" ",1,"FA040SITU")
	Return Nil
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Salva a integridade dos campos de Bancos de Dados 			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("Sx2")
MsSeek("SL2")
dbSelectArea("Sx3")
dbSetOrder(1)
MsSeek( "SL2" )
While !Eof() .AND. (x3_arquivo == "SL2")
	IF x3Uso(x3_usado) .AND. cNivel >= x3_nivel .AND. alltrim(x3_campo)<>"L2_NUM"
		If cPaisLoc <> "BRA"
			//Estes campos nao aparecerao em outros paises
			If Alltrim(X3_CAMPO) == "L2_ICMSRET" .OR. Alltrim(X3_CAMPO) == "L2_BRICMS"
				dbSkip()
				Loop
			Endif
		Endif
		
		nUsado++
		AADD(aHeader,{ TRIM(x3titulo()), x3_campo, x3_picture,;
		x3_tamanho, x3_decimal, x3_valid,;
		x3_usado, x3_tipo, x3_arquivo } )
		Aadd( aCampos, x3_campo )
		If cPaisLoc <> "BRA"
			Do Case
				Case Alltrim(X3_CAMPO)	==	"L2_VRUNIT"
					nPosVlrUni := Len(aHeader)
				Case Alltrim(X3_CAMPO)	==	"L2_VLRITEM"
					nPosVlrTot := Len(aHeader)
				Case Alltrim(X3_CAMPO)	==	"L2_QUANT"
					nPosQuant  := Len(aHeader)
				Case Alltrim(X3_CAMPO)	==	"L2_DESCPRO"
					nPosDescPro:= Len(aHeader)
			EndCase
		Endif
	EndIf
	dbSkip( )
End

While .T.
	nImpostos := 0
	nImpsInc  := 0
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Verifica nivel do desconto.							 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea( "SX3" )
	dbSetOrder( 2 )
	MsSeek( "L1_DESCONT" )
	dbSetOrder( 1 )
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Aceita o cabe‡alho do Orcamento 							³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	M->L1_NUM     := SPACE( TamSX3( "L1_NUM" )[1] )
	M->L1_DOC     := SL1->L1_DOC
	M->L1_NROPCLI := SPACE( TamSX3( "L1_NROPCLI" )[1] )
	M->L1_VLRTOT  := 0
	M->L1_CLIENTE := GetMv( "MV_CLIPAD" )
	M->L1_DESCONT := 0
	M->L1_VEND	  := GetMv( "MV_VENDPAD" )
	M->L1_DTLIM   := dDataBase
	M->L1_VLRLIQ  := 0
	M->L2_VALDESC := 0
	M->L2_DESC	  := 0
	
	dbSelectArea( "SL1" )
	
	M->L1_NUM	  := SL1->L1_NUM
	M->L1_NROPCLI := SL1->L1_NROPCLI
	M->L1_VLRTOT  := SL1->L1_VLRTOT
	M->L1_CLIENTE := SL1->L1_CLIENTE
	M->L1_DESCONT := SL1->L1_DESCONT
	M->L1_VEND	  := SL1->L1_VEND
	M->L1_DTLIM   := SL1->L1_DTLIM
	M->L1_VLRLIQ  := SL1->L1_VLRLIQ
	M->L1_LOJA    := SL1->L1_LOJA
	M->L1_VALISS  := SL1->L1_VALISS
	
	If SL1->(FieldPos("L1_ABTOPCC")) > 0
		M->L1_ABTOPCC := SL1->L1_ABTOPCC
	EndIf
	
	dbSelectArea( "SL2" )
	dbSetOrder(1)
	MsSeek( xFilial("SL2")+M->L1_NUM )
	
	Private aCols[nCnt][nUsado]
	nUsado := 0
	nCnt:=0
	
	While !Eof( ) .AND. L2_FILIAL+L2_NUM==xFilial("SL1")+M->L1_NUM .AND. SL2->L2_STATUS <> "D"
		nCnt++
		nUsado:=0
		dbSelectArea("SX3")
		MsSeek( "SL2" )
		While !Eof() .AND. X3_ARQUIVO == "SL2"
			If x3Uso(X3_USADO) .AND. cNivel >= X3_NIVEL .AND. AllTrim(X3_CAMPO) <> "L2_NUM"
				If cPaisLoc <> "BRA"
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//|    Estes campos nao devem aparecer em outros paises                                                     |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					
					If Alltrim(X3_CAMPO) == "L2_ICMSRET" .OR. Alltrim(X3_CAMPO) == "L2_BRICMS"
						dbSkip()
						Loop
					Endif
				Endif
				
				nUsado++
				If X3_CONTEXT <> "V"
					aCOLS[nCnt][nUsado] := &("SL2->"+X3_CAMPO)
				Else
					aCOLS[nCnt][nUsado] := CriaVar(X3_CAMPO)
				EndIf
			EndIf
			dbSkip( )
		End
		If cPaisLoc <> "BRA"
			If nPosDescPro > 0
				aCols[nCnt][nPosVlrTot] += aCols[nCnt][nPosDescPro]
			EndIf
			aCols[nCnt][nPosVlrUni] := Round(aCols[nCnt][nPosVlrTot]/aCols[nCnt][nPosQuant],nDecs)
			aPropImp :=	TesImpInf(SL2->L2_TES)
			For nY := 1	To Len(aPropImp)
				If ( aPropImp[nY][3]=="1" )
					nImpostos += SL2->(FieldGet(FieldPos("L2_"+Substr(aPropImp[nY][2],4,7) )))
				ElseIf ( aPropImp[nY][3]=="3" )
					nImpsInc  += SL2->(FieldGet(FieldPos("L2_"+Substr(aPropImp[nY][2],4,7) )))
				Endif
			Next nY
		Endif
		dbSelectArea( "SL2" )
		dbSkip( )
	End
	
	If Len(aCols) == 0
		Help(" ","1","TROCADO")
		SetCursor(0)
		Return( Nil )
	EndIf
	
	nOpca := 0
	If cPaisloc <> "BRA"
		M->L1_VLRLIQ := M->L1_VLRTOT-nImpostos+M->L1_DESCONT
	Endif
	
	DEFINE MSDIALOG oDlg TITLE cCadastro From 9,0 TO 28,80 OF oMainWnd
	@1.4,.8 Say OemToAnsi(STR0011)	// Or‡amento:
	@1.4, 5 MSGet M->L1_NUM When .F.
	
	@1.4,10 Say OemToAnsi(STR0023)	// 'Documento    :'
	@1.4,15 MSGet M->L1_DOC When .F.
	
	If cPaisLoc == "GUA" .AND. lGeraNCC
		@ 20, 185 CHECKBOX oCheckNFMan VAR lNFManual PROMPT OemToAnsi(STR0072);	 //"NCC Manual?"
		OF oDlg SIZE 60,08
	EndIf
	
	@ 100, 005 TO 142, 125 OF oDlg PIXEL
	@ 100, 127 TO 142, 235 OF oDlg PIXEL
	@ 100, 237 TO 142, 317 OF oDlg PIXEL
	
	@ 105, 008 SAY xPadL(OemToAnsi(STR0012), 36) SIZE 36, 7 OF oDlg PIXEL	// Seu Pedido:
	@ 105, 038 MSGET M->L1_NROPCLI When .F. SIZE 30, 08 OF oDlg PIXEL
	@ 117, 008 SAY xPadL(OemToAnsi(STR0013), 24) SIZE 30, 7 OF oDlg PIXEL	// Cliente:
	@ 117, 038 MSGET M->L1_CLIENTE When .F. SIZE 85, 08 OF oDlg PIXEL
	@ 129, 008 SAY xPadL(OemToAnsi(STR0014), 31) SIZE 31, 7 OF oDlg PIXEL	// Vendedor:
	@ 129, 038 MSGET M->L1_VEND When .F. SIZE 30, 08 OF oDlg PIXEL
	
	@ 105, 130 SAY xPadL(OemToAnsi(STR0015), 66) SIZE 66, 7 OF oDlg PIXEL	// Total de Mercadorias:
	@ 117, 130 SAY xPadL(OemToAnsi(STR0016), 36) SIZE 36, 7 OF oDlg PIXEL	// Desconto:
	
	If cPaisLoc == "BRA"
		@ 117, 185 MSGET M->L1_DESCONT When .F. Picture "@E 999,999.99" SIZE 46, 08 OF oDlg PIXEL
	Else
		@ 117, 185 MSGET M->L1_DESCONT When .F. Picture PesqPict("SL1","L1_DESCONT") SIZE 50, 08 OF oDlg PIXEL
	EndIf
	
	If cPaisLoc == "BRA"
		
		@ 105, 240 SAY xPadL(OemToAnsi(STR0017), 25) SIZE 25, 7 OF oDlg PIXEL	// L¡quido:
		@ 117, 240 SAY xPadL(OemToAnsi(STR0018), 28) SIZE 25, 7 OF oDlg PIXEL	// Validade:
		
		nAbatimentos := 0
		
		If SL1->(FieldPos("L1_ABTOPCC")) > 0
			M->L1_VLRLIQ -= M->L1_ABTOPCC
			nAbatimentos += M->L1_ABTOPCC
		EndIf
		
		If ( Posicione("SA1",1,xFilial("SA1")+M->L1_CLIENTE+M->L1_LOJA,"SA1->A1_RECISS") == "1" .AND. GetNewPar("MV_DESCISS", .F.) )
			M->L1_VLRLIQ -= M->L1_VALISS
			nAbatimentos += M->L1_VALISS
		EndIf
		
		@ 105, 185 MSGET M->L1_VLRTOT When .F. Picture "@E 999,999.99" SIZE 46, 08 OF oDlg PIXEL
		@ 105, 270 MSGET M->L1_VLRLIQ When .F. Picture "@E 999,999.99" SIZE 46, 08 OF oDlg PIXEL
		@ 117, 270 MSGET M->L1_DTLIM  When .F.                         SIZE 46, 08 OF oDlg PIXEL
		@ 129, 130 Say xPadL(OemToAnsi(STR0076), 66) Size 66, 7 OF oDlg PIXEL   // Abatimentos:
		@ 129, 185 MSGET nAbatimentos When .F. Picture "@E 999,999.99" SIZE 50, 08 OF oDlg PIXEL
		
	Else
		
		@ 105, 240 SAY xPadL(OemToAnsi(STR0018), 28) SIZE 28, 7 OF oDlg PIXEL	// Validade:
		@ 129, 130 SAY xPadL(OemToAnsi(STR0017), 25) SIZE 25, 7 OF oDlg PIXEL	// L¡quido:
		
		@ 105, 185 MSGET M->L1_VLRLIQ When .F. Picture PesqPict("SL1","L1_VLRLIQ") SIZE 50, 08 OF oDlg PIXEL
		@ 129, 185 MSGET M->L1_VLRTOT When .F. Picture PesqPict("SL1","L1_VLRTOT") SIZE 50, 08 OF oDlg PIXEL
		@ 105, 267 MSGET M->L1_DTLIM When .F. SIZE 49, 08 OF oDlg PIXEL
		
		@ 117, 240 SAY xPadL(OemToAnsi(STR0027), 28) SIZE 28, 7 OF oDlg PIXEL	// Imp. Inc.:
		@ 117, 267 MSGET nImpsInc When .F. Picture PesqPict("SL1","L1_VALIMP1") SIZE 49, 08 OF oDlg PIXEL
		
		@ 129, 240 SAY xPadL(OemToAnsi(STR0028), 28) SIZE 28, 7 OF oDlg PIXEL 	// Imp. Disc.:
		@ 129, 267 MSGET nImpostos When .F. Picture PesqPict("SL1","L1_VALIMP1") SIZE 49, 08 OF oDlg PIXEL
		
	Endif
	
	oGet := MSGetDados():New(34,5,100,315,nOpcx,,,"")
	ACTIVATE MSDIALOG oDlg ON INIT EnchoiceBar(oDlg,{|| nOpca := 1,oDlg:End()},{|| nOpca := 2,oDlg:End()})
	
	If nOpcA == 1
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	    //³Funcao para exclusao do historico do pontuacao do Cliente.                 ³
	    //ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	
		If FindFunction("lj140EXF") //Funcao utilizada no Venda Assistida (CRDXFUNE.PRW)
			LJ140EXF()
		EndIf
		
		If (ExistTemplate("LJ140EXC"))
			ExecTemplate("LJ140EXC",.F.,.F.)
		EndIf
		If (ExistBlock("LJ140EXC"))
			ExecBlock("LJ140EXC",.F.,.F.)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Determinar a numeracao e o motivo da NCC - Loc. Guatemala                 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If cPaisLoc == "GUA" .AND. lVendido
			If lGeraNCC .AND. !Lj140Manual()
				Return NIL
			EndIf
			If !Lj021MotDev(@cDescMot)
				Return NIL
			EndIf
			If lGeraNCC
				If !LJ021Nota(.T.)
					Return NIL
				EndIf
			EndIf
		EndIf
		
		Begin Transaction
		
		If !lj140chkres(lVendido)
			Return Nil
		Endif
		If lVendido
			xRet  := lj140Grava(,cDescMot)
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     No estado da Amazonia nao e permitido reutilização de orcamento de um cupom cancelado.              |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !lVendido .OR. FunName() = "LOJA220" .OR. LjAnalisaLeg(7)[1]
			If !LjAnalisaLeg(6)[1] .OR. FunName() = "LOJA220" .OR. cPaisLoc <> "BRA"
				lj140ExcOrc()
			Else
				If lFiscal
					If Lj140ICOrc(aCols)
						lj140ExcOrc()
					Else
						MsgStop( STR0030 + Chr(10)+ Chr(13) + ; 		//"Existem problemas com a impressora fiscal"
						STR0059, STR0047) 						//"Não foi possível imprimir e cancelar o orçamento!" //Atenção
					EndIf
				Else
					MsgStop(STR0060 + Chr(10)+ Chr(13) + ;				//"Para cancelar o orcamento o usuario deve ser fiscal"
					STR0063 + Chr(10)+ Chr(13) + ;			//"Por exigencia da Legislação Vigente no Estado, para todo orçamento cancelado, "
					STR0064, STR0047)					    //"deve ser impresso um cupom fiscal e seu cancelamento"
				EndIf
			EndIf
		ENDIF
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|     Verifica se o Siga Loja esta integrado com o Gestão Hospitalar                                      |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If GetNewPar("MV_LOJAHSP",.F.)
			HS_ExclRAt(M->L1_NUM, lVendido)
		EndIf
		
		End Transaction
		
		If cPaisLoc == "GUA" .AND. lGeraNCC .AND. IIf(ValType(xRet)=="L",xRet,.F.)
			Lj021Imprime()
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|      "Aten‡„o"###"Foi gerada a Nota de Credito "###"Ok"                                                 |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Aviso(STR0006,STR0070+AllTrim(cSerieDev)+"-"+cNumNFDev,{STR0048})
		EndIf
	Else
		dbSelectArea("SL1")
	End
	Exit
End
Return nOpca

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³lj140Grava³ Autor ³ Antonio Maniero Jr.   ³ Data ³ 02/01/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³Atualiza‡„o de arquivos.                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³lj140Grava( )                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ DATA     ³ BOPS ³Prograd.³ALTERACAO                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 20/07/05 ³083898³Hanna C ³Gera um registro no SF3 com NF Cancelada,   ³±±
±±³          ³      ³        ³caso este ainda nao exista, somente se nao  ³±±
±±³          ³      ³        ³utilizar Mapa Resumo (MAPARES <> "O")       ³±±
±±³ 26/09/05 ³086909³Hanna C ³De acordo com o BOPS 083898, gerar SF3      ³±±
±±³          ³      ³        ³quando o MV_MAPARES == "N"                  ³±±
±±³          ³      ³        ³                                            ³±±
±±³ 29/09/05 ³084541³Magh M. ³Nao estava atualizando o Saldo Bancario pq  ³±±
±±³          ³      ³        ³o registro do SE5 era excluido linhas antes ³±±
±±³          ³      ³        ³da chamada da funcao AtuSalBco()            ³±±
±±³ 03/11/05 ³088403³Henry F ³Alteracao para considerar os campos do SL1  ³±±
±±³          ³      ³        ³na geracao do SF3 (L1_DOC, L1_SERIE, L1_PDV ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140Grava(lJob,cDescMot)
Local nContador	   := 0
Local aCheques 	   := {}
Local lVendTef 	   := .F.
Local lTefOk       := .T.
Local cChave
Local cNumNota     := SL1->L1_DOC
Local cSerNota     := SL1->L1_SERIE
Local aParc        := {}
Local nI           := 0
Local cChaveSE1    := ""
Local cNumNfCup    := ""
Local cSerNfCup    := ""
Local lNfCup       := .F.
Local lMvFisNota   := GetMv("MV_FISNOTA")
Local lCrdxInt     := CrdxInt()                //Controla se tem integracao com SIGACRD
Local cCpoTroco    := ""
Local aItensSD2    := {}
Local aItensSF2    := {}
Local aRetCrd      := {}                       //Array  de retorno do cancelamento do contrato - integracao SIGACRD
Local cTESSaida    := ""
Local cCondPag     := ""
Local lFoundNF     := .F.
Local lBaixaAut    := .F.
Local lCartao      := .T.
Local lLjD1TesT    := (ExistBlock("LJD1TesT"))						//Verifica a existencia do PE para tratamento de TES
Local lCancelouCRD := .F.                                      // Indica se o contrato ja foi cancelado
Local cTESTroca	   := GetMv("MV_TESTROCA")							//TES padrao para o processo de troca
Local aOtimizacao  := {}						//Array utilizada na MATXFIS
Local lDelSE5      := .F. //variavel que verificara se o SE5 foi excluido                               
Local lAchouSF2	   := .F.											// Variavel de retorno se encontrou o SF2 ou nao

Default lJob       := .F.
Default cDescMot   := ""

If Type("LANULASF3")=="U"
	Private lAnulaSF3 := .F.
EndIf

cChave := LJPREFIXO() + SL1->L1_DOC
iRet := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|   Caso lJob seja true significa que a rotina esta sendo executada atraves                               |
//|   de um Job. Com certeza se trata de um usuario fiscal e todas as validacoes                            |
//|   fiscais foram realizadas, no Front Office.                                                            |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lJob
	If lFiscal
		If Empty( SL1->L1_PDV ) .AND. !lMvFisNota
			MsgInfo(OemToAnsi(STR0035)) //'Usuário não altorizado a excluir NF'
			Return( .F. )
		Endif
	Else
		If !Empty( SL1->L1_PDV )
			MsgInfo(OemToAnsi(STR0036)) //'Usuário não altorizado a excluir Cupon Fiscal'
			Return( .F. )
		Endif
	Endif
EndIf


If !lJob .AND. lUsatef .AND. SL1->L1_VENDTEF == "S"
	lVendTef := .T.
	If !Lj010Autor("TEF-EXC")
		Return NIL
	EndIf
ElseIf !lJob .AND. !lUsatef .AND. SL1->L1_VENDTEF == "S"
	lVendTef := .F.
	Help(" ",1,"NOUSERTEF")
	Return NIL
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza‡„o de Contas a Receber e Saldo de Caixa			 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(SL1->L1_CONFVEN,6,1) <> "N"  // - Posicao de Geracao Titulo Financeiro
	cCondPag  := AllTrim(SL1->L1_CONDPG)
	If cPaisLoc <> "BRA" .AND. cCondPag <> "CN"
		SE4->(DbSetOrder(1))
		If SE4->(MsSeek(xFilial("SE4")+cCondPag))
			If SE4->(FieldPos( "E4_BXTITAV" )) > 0
				lBaixaAut := SE4->E4_BXTITAV == "1"
			Endif
		EndIf
	EndIf
	dbSelectArea("SE1")
	dbSetOrder(1)
	If MsSeek(xFilial("SE1")+ cChave )
		dbSelectArea("SE1")
		While !Eof() .AND. xFilial("SE1")==SE1->E1_FILIAL .AND. cChave == E1_PREFIXO+E1_NUM
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Inserido origem para evitar que selecione registro incluido  ³
			//³ por outro modulo                                             ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If AllTrim(Upper(SE1->E1_ORIGEM))$"LOJA010/LOJA220/LOJA701/RPC"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//| Inseri IsMoney para considerar titulos pagos em qualq. moeda em dinheiro- J. Aurelio-Localiz.-08/07/02  |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If Round(NoRound(E1_VALOR,3),2) <> Round(NoRound(E1_SALDO,3),2) .AND. !IsMoney(E1_TIPO) .AND.;
					!lBaixaAut
					Help(" ",1,"TITULOBAIXADO")
					Return .F.
				EndIf
				If SE1->E1_TIPO $ IIF(Type('MVCHEQUES')=='C',MVCHEQUES,MVCHEQUE)
					Aadd( aCheques , SE1->E1_PREFIXO+SE1->E1_NUM )
				EndIf
			EndIf
			dbSkip( )
		End
	EndIf
EndIf

dbSelectArea( "SF2" )
dbSetOrder(1)
lAchouSF2	:=	MsSeek( xFilial("SF2") + cNumNota + SL1->L1_SERIE )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//| Verifica se existe NF para o cupom. Caso exista armazena o numero e a serie para futura exclusao...     |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If ((lJob .OR. lFiscal) .AND. (SF2->F2_ECF == "S")) .AND. !Empty(SF2->F2_NFCUPOM) .AND. lAchouSF2
	cSerNfCup := SubStr(SF2->F2_NFCUPOM,1,TamSx3("F2_SERIE")[1])
	cNumNfCup := SubStr(SF2->F2_NFCUPOM,4,TamSx3("F2_DOC")[1])
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica se cheque ja' foi depositado                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If Substr(SL1->L1_CONFVEN,1,1)  <> "N" // Posicao de Aceitacao de Numero de Cheque
	dbSelectArea("SEF")
	dbSetOrder(3)
	If Len(aCheques) > 0  // 24/05/96			// aCheques[] prefixo+titulo
		If !Empty(SL1->L1_CHEQUES) .AND. MsSeek(xFilial("SL1")+aCheques[1])
			nContador := 1
			While !Eof() .AND. xFilial("SEF")==SEF->EF_FILIAL .AND. nContador <= Len(aCheques)
				If !Empty(SEF->EF_DEPOSIT)
					Help(" ",1,"CHEQUEMOV")
					Return .F.
				EndIf
				nContador++
				dbSkip()
			End
		EndIf
	EndIf  // Fim de Len(acheques)
EndIf

If lFiscal .AND. (SF2->F2_ECF == "S") .AND. lAchouSF2
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Cancelamento do £ltimo Cupom Fiscal						 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If IFCancCup( nhdlECF ) <> 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//|    "N„o foi possível efetuar a exclus„o"                                                                |
		//|    "Existem problemas com a impressora fiscal"                                                          |
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		MsgInfo(OemToAnsi(STR0029) + Chr(10) + Chr(13) + OemToAnsi(STR0030))
		Return NIL
	EndIf
EndIf

If Substr(SL1->L1_CONFVEN,1,1)  <> "N" // Posicao de Aceitacao de Numero de Cheque
	If Len(aCheques) > 0  // 24/05/96			// aCheques[] prefixo+titulo
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza‡„o dos arquivos. 											  ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If !Empty(SL1->L1_CHEQUES) .AND. MsSeek(xFilial("SEF")+aCheques[1])
			nContador := 1
			While !Eof() .AND. xFilial("SEF")==SEF->EF_FILIAL .AND. nContador <= len(aCheques)
				lAvanca := .T.
				If SEF->EF_CART == "R"
					Reclock( "SEF",.F.,.T.)
					dbDelete()
					MSUnLock()
					dbSkip( )
				ENDIF
				nContador++
			End
		EndIf
	EndIf  // Fim de Len(acheques)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|  Se usar o Tef ira excluir a venda na administradora antes                                              |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If !lJob .AND. lVendTef
	lTefOk := LOJA010T( "X" )
	If ! Valtype(lTefOK) == "L"
		lTefOK := .F.
	Endif
	If ! lTefOk
		Help(" ",1,"TEFOK")
		LOJA010T( "F", "N" )
		Return NIL
	EndIf
EndIf

If lTefOk
	If !lJob .AND. lVendTef
		RecLock("SL1",.F.)
		SL1->L1_DOCCANC := aTefDados[1][6]
		SL1->L1_DATCANC := aTefDados[1][12]
		SL1->L1_HORCANC := aTefDados[1][7]
		MsUnLock()
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Antes de Excluir Comissao - Verifica o Parametro de Geracao de Comissao - 19/12/95³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If Substr(SL1->L1_CONFVEN,11,1) <> "N" // Posicao do Parametro de Geracao de Comissao
		
		dbSelectArea("SE3")
		If MsSeek(xFilial("SE3")+ cChave )
			While !Eof() .AND. !Empty(SL1->L1_VEND) .AND.;
				cChave == SE3->E3_PREFIXO + SE3->E3_NUM
				
				If !Empty( SE3->E3_DATA )
					Help( " ",1,"A140COMIS")
					dbSkip()
					Loop
				EndIf
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Atualiza‡„o de Comiss”es												  ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				RecLock("SE3",.F.,.T.)
				dbDelete( )
				MSUnLock()
				dbSkip()
			End
		EndIf
	EndIf
	
	cNumNota := SL1->L1_DOC
	cSerNota := SL1->L1_SERIE
	While .T.
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Atualiza‡„o dos Arquivos do Faturamento ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pontos de Entrada  						³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If ExistTemplate("LJ140DEL")
			ExecTemplate("LJ140DEL",.F.,.F.)
		EndIf
		
		If ExistBlock("LJ140DEL")
			ExecBlock("LJ140DEL",.F.,.F.)
		EndIf
		
		dbSelectArea("SF3")
		dbSetOrder(5)
		If MsSeek( xFilial("SF3") + cSerNota + cNumNota )
			While !Eof() .AND. xFilial("SF3")+cSerNota+cNumNota == SF3->F3_FILIAL+SF3->F3_SERIE+SF3->F3_NFISCAL
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Excluir apenas Nota Fiscal de Saida	    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If !(Substr(SF3->F3_CFO,1,1) >= "5")
					dbSkip()
				Endif
				RecLock("SF3",.F.)
				If IIf(cPaisLoc<>"BRA",lAnulaSF3,.T.)
					SF3->F3_DTCANC := dDatabase
					SF3->F3_OBSERV := "NF CANCELADA"
					If cPaisLoc == "GUA"
						SF3->F3_OBSERV   := cDescMot
					EndIf
				Else
					DbDelete()
				EndIf
				MsUnLock()
				dbSkip()
			End
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//| Caso nao tenha sido gerado F3, deve registrar o cancelmento desta NF, sempre que o MV_MAPARES == "N"  |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If cPaisLoc == "BRA" .AND. ( Empty(SL1->L1_PDV) .And. !Empty(SL1->L1_DOC+SL1->L1_SERIE) ) .AND. lAchouSF2
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Carrega a Nota Fiscal SF3 referente a Notas Fiscais de Entrada³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MaFisIniNF(2,SF2->(Recno()),@aOtimizacao,"SF2",.T.)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Inicializa a gravacao nas funcoes Fiscais               ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				MaFisWrite()
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Efetua a gravacao dos registros referente a Nota no SF3 ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				dbSelectArea("SF2")
				
				MaFisAtuSF3(1,"S",SF2->(Recno()),"SF2")
				RecLock("SF3",.F.)
				SF3->F3_DTCANC	:= dDatabase
				SF3->F3_OBSERV	:= "NF CANCELADA"
				SF3->(MsUnLock())
				MaFisEnd()

			Endif
		EndIf
		
		dbSelectArea("SD2")
		dbSetOrder(3)
		If MsSeek(xFilial("SD2")+cNumNota+cSerNota)
			While !Eof() .AND.	(xFilial("SD2") == D2_FILIAL) .AND.;
				(D2_DOC == cNumNota .AND. D2_SERIE == cSerNota)
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|    Considerar somente os itens de NF                                                                    |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cPaisLoc <> "BRA"
					If SD2->D2_TIPODOC <> "01"
						DbSkip()
						Loop
					EndIf
				EndIf
				
				If lNfCup .OR. (SD2->D2_PDV == SL1->L1_PDV)
					cTESSaida  := SD2->D2_TES
					If !lNfCup .AND. SF4->(MsSeek(xFilial("SF4")+SD2->D2_TES))
						If SF4->F4_ESTOQUE == "S"
							If SB2->( MsSeek(cFilial + SD2->D2_COD + SD2->D2_LOCAL ) )
								
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//|     Verifica se o cliente utiliza tratamento específico para o controle de TES         |
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								If lLjD1TesT
									aRet := ExecBlock("LJD1TesT",.F.,.F.,{cTESTroca,SD2->D2_COD,0})
									If ValType(aRet) == "A"
										cTESTroca := aRet[1]
									EndIf
								EndIf
								
								RecLock("SD2",.F.)
								Replace D2_TES With cTESTroca
								
								aCMSaida := PegaCusD2()
								
								B2AtuComD2( aCMSaida, 1,,.F.)
								SB2->(MsUnLock())
								
								If !( SD2->D2_TIPO $ "DBCIP" )
									dbSelectArea("SB3")
									dbSetOrder(1)
									If ( MsSeek(xFilial("SB3")+SD2->D2_COD) )
										RecLock("SB3")
									Else
										RecLock("SB3",.T.)
										SB3->B3_FILIAL := xFilial("SB3")
										SB3->B3_COD    := SD2->D2_COD
									EndIf
									FieldPut(FieldPos("B3_Q"+StrZero(Month(SD2->D2_EMISSAO),2)),FieldGet(FieldPos("B3_Q"+StrZero(Month(SD2->D2_EMISSAO),2)))-SD2->D2_QUANT)
									SB3->B3_MES := SD2->D2_EMISSAO
									MsUnLock()
								EndIf
								
							EndIf
						EndIf
					EndIf
					
					dbSelectArea( "SD2" )
					
					If cPaisLoc == "GUA"
						Aadd(aItensSD2,{SD2->D2_ITEM,SD2->D2_COD,SD2->D2_UM,SD2->D2_SEGUM,;
						SD2->D2_QUANT,SD2->D2_PRCVEN,SD2->D2_TOTAL,SD2->D2_LOCAL,;
						cTESSaida,SD2->D2_CF,SD2->D2_CLIENTE,SD2->D2_LOJA,;
						SD2->D2_GRUPO,SD2->D2_PRUNIT,SD2->D2_DOC,SD2->D2_SERIE,;
						SD2->D2_CUSTO1,SD2->D2_CUSTO2,SD2->D2_CUSTO3,SD2->D2_CUSTO4,;
						SD2->D2_CUSTO5})
					EndIf
					
					RecLock("SD2",.F.,.T.)
					dbDelete()
					MsUnlock()
				EndIf
				dbSkip()
			End
		EndIf
		
		dbSelectArea("SF2")
		dbSetOrder(1)
		If MsSeek(xFilial("SF2")+cNumNota+cSerNota)
			If cPaisLoc <> "BRA"
				lFoundNF  := .F.
				While !Eof() .AND. xFilial("SF2") == SF2->F2_FILIAL .AND. cNumNota+cSerNota ==;
					SF2->F2_DOC + SF2->F2_SERIE .AND. !lFoundNF
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//|    Verifica se eh NF                                                                                    |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					If SF2->F2_TIPODOC <> "01"
						DbSkip()
						Loop
					EndIf
					
					lFoundNF  := .T.
				End
			EndIf
			cNumNota := SF2->F2_NEXTDOC
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Exclui a Nf gerada para um determinado cupom, caso a mesma exista...                                 |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Empty(cNumNota) .AND. !Empty(cNumNfCup)
				cNumNota := cNumNfCup
				cSerNota := cSerNfCup
				cNumNfCup:= ""
				cSerNfCup:= ""
				lNfCup   := .T.
			EndIf
			If cPaisLoc <> "BRA"
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|     Armazenar os dados dos itens para gerar a NCC - Loc. Guatemala                                      |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				
				Aadd(aItensSF2,{SF2->F2_DOC,SF2->F2_SERIE,SF2->F2_CLIENTE,SF2->F2_LOJA,;
				SF2->F2_VALBRUT,SF2->F2_VALMERC,SF2->F2_MOEDA,SF2->F2_TXMOEDA})
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Cancela a baixa dos titulos com pagamento a vista.³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				SE4->(DbSetOrder(1))
				SE4->(MsSeek(xFilial("SE4")+SF2->F2_COND))
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|     Estornar a baixa dos titulos baixados automaticamente                                           |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				CaBxAutSE1(SF2->F2_COND,"SE1")
			EndIf
			RecLock("SF2",.F.,.T.)
			dbDelete()
			MsUnlock()
		Else
			cNumNota := Space(TamSx3("L1_DOC")[1])
		EndIf
		
		If !Empty(cNumNota)
			Loop
		Endif
		Exit
	End
	
	If Substr(SL1->L1_CONFVEN,6,1) <> "N"
		dbSelectArea("SE1")
		If MsSeek(xFilial("SE1")+cChave)
			SAE->( dbSetOrder( 1 ) )
			While !Eof() .AND. xFilial("SE1")==SE1->E1_FILIAL .AND. cChave == SE1->(E1_PREFIXO+E1_NUM)
				
				lCartao := .F.
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se a origem for pelo financeiro, nao efetua a exclusao do titulo    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If "FIN"$SE1->E1_ORIGEM
					SE1->(DbSkip())
					Loop
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|    Se o título não for do cliente verifica se é de alguma administradora financeira,                    |
				//|    se for não roda a rotina de atualização do calso do cliente e pula para o próximo registro.          |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If SL1->L1_CLIENTE+SL1->L1_LOJA <> SE1->E1_CLIENTE + SE1->E1_LOJA
					If SAE->( MsSeek( xFilial( "SAE" ) + SubStr( SE1->E1_CLIENTE,1,3 ) ) )
						lCartao := .T.
					EndIf
				EndIf
				
				If !IsMoney(E1_TIPO) .AND. ! lCartao .AND. ! (GetMv( "MV_CLIPAD" )+GetMv( "MV_LOJAPAD" ) == SL1->L1_CLIENTE+SL1->L1_LOJA)
					SA1->(DbSetOrder(1))
					SA1->(MsSeek(xFilial("SA1")+SL1->L1_CLIENTE+SL1->L1_LOJA))
					AtuSaldup("-",SE1->E1_VALOR,SE1->E1_MOEDA,SE1->E1_TIPO,,SE1->E1_EMISSAO)
				Endif
				
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|   Eh necessario armazenar as parcelas para o tratamento do SE5                                          |
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If cPaisLoc <> "BRA"
					AAdd(aParc,{SE1->E1_PARCELA,SE1->E1_TIPO})
					If GetMV("MV_LJTRLOC")  //Rotina de Troco localizada ativa
						cChaveSE1 := SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+Space(Len(SE5->E5_TIPO))+;
						SE1->E1_CLIENTE+SE1->E1_LOJA
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//|   Excluir os registros de troco correspondentes                                                 |
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						DbSelectArea("SE5")
						DbSetOrder(7)
						If MsSeek(xFilial("SE5")+cChaveSE1)
							While !Eof() .AND. xFilial("SE5") == SE5->E5_FILIAL .AND. cChaveSE1 == SE5->E5_PREFIXO+;
								SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO+SE5->E5_CLIFOR+SE5->E5_LOJA
								
								If SE5->E5_MOEDA <> "TC" .OR. SE5->E5_TIPODOC <> "VL" .OR.;
									SE5->E5_RECPAG <> "P"
									DbSkip()
									Loop
								EndIf
								

								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³Se for dinheiro devera atualizar o Saldo Bancario³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ								
								If IsMoney(SE1->E1_TIPO) 		
 									//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
									//³ Atualiza saldo do BANCO Caixa ³
									//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
									AtuSalBco(SE5->E5_BANCO,SE5->E5_AGENCIA,SE5->E5_CONTA,dDataBase,SE5->E5_VALOR,"-")			
							    	lDelSE5:= .T.
							    Endif	                                  
							    
								RecLock("SE5",.F.)
								DbDelete()
								MsUnlock()
								DbSkip()
							End
						EndIf
					EndIf
				Else
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//|    Excluir os registros de movimentação bancaria - Integridade Referencial                   |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					cChaveSE1 := SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+SE1->E1_TIPO+SE1->E1_CLIENTE+SE1->E1_LOJA
					
					DbSelectArea("SE5")
					DbSetOrder(7)
					If MsSeek(xFilial("SE5")+cChaveSE1)
						While !Eof() .AND. xFilial("SE5") == SE5->E5_FILIAL .AND. ;
							cChaveSE1 == SE5->E5_PREFIXO+SE5->E5_NUMERO+SE5->E5_PARCELA+SE5->E5_TIPO+SE5->E5_CLIFOR+SE5->E5_LOJA
				
							

							//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
							//³Se for dinheiro devera atualizar o Saldo Bancario antes de excluir o registro do SE5³
							//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ					
							If IsMoney(SE1->E1_TIPO)
								//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
								//³ Atualiza saldo do BANCO Caixa ³
								//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
								AtuSalBco(SE5->E5_BANCO,SE5->E5_AGENCIA,SE5->E5_CONTA,dDataBase,SE5->E5_VALOR,"-")
								lDelSE5:= .T.
				            Endif
				
							RecLock("SE5",.F.)
							DbDelete()
							MsUnlock()
							DbSkip()
						End
					EndIf
					
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//|    Excluir registro do Contas a Pagar (quando houver Taxa de ISS)                                       |
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					dbSelectArea("SE2")
					dbSetOrder(1)
					If MsSeek(xFilial("SE2")+SE1->E1_PREFIXO+SE1->E1_NUM+SE1->E1_PARCELA+"TX")
						If Alltrim(SE2->E2_NATUREZ) == "ISS"
							RecLock("SE2",.F.)
							DbDelete()
							MsUnlock()
						Endif
					Endif
					
				EndIf
				If lCrdxInt .AND. !lCancelouCRD .AND. SE1->(FieldPos("E1_NUMCRD")) > 0
					If !Empty(SE1->E1_NUMCRD)
						aRetCrd      := aClone(CrdxVenda( "3"  ,{"",""}  ,SE1->E1_NUMCRD  ,.F.  ,;
						NIL  ,NIL ))
						lCancelouCRD := .T.
					EndIf
				EndIf
				Reclock("SE1")
				SE1->(dbDelete())
				MSUnLock()
				SE1->(dbSkip())
			End
		EndIf
		                                       
		

		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Se for Dinheiro e SE5 nao estiver sido excluido acima³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ		
		If SL1->L1_DINHEIRO <> 0 .AND. !lDelSE5
			dbSelectArea("SE5")
			dbSetOrder(2)
			If cPaisLoc == "BRA"
				If MsSeek(xFilial() + "LJ" + cChave + Space(TamSX3("E5_PARCELA")[1]) + GetMv("MV_SIMB1") + " ")
					RecLock("SE5",.F.,.T.)
					dbDelete()
					MsUnlock()
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Atualiza saldo do BANCO Caixa ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					AtuSalBco(SE5->E5_BANCO,SE5->E5_AGENCIA,SE5->E5_CONTA,dDataBase,SE5->E5_VALOR,"-")
					
				EndIf
			Else
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//|Devido ao tratamento de multi-moeda uma venda pode gerar mais de um registro em Mov. Bancario p/ dinheiro|
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				For nI := 1 To Len(aParc)
					If MsSeek(xFilial() + "LJ" + cChave + aParc[nI,1] + aParc[nI,2])
						//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
						//³ Atualiza saldo do BANCO Caixa ³
						//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
						AtuSalBco(SE5->E5_BANCO,SE5->E5_AGENCIA,SE5->E5_CONTA,dDataBase,SE5->E5_VALOR,"-")
						RecLock("SE5",.F.,.T.)
						dbDelete()
						MsUnlock()
					EndIf
				Next nI
			EndIf
			dbSetOrder(1)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Realiza a exclusao do SE1 apos o SE5 ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SE1")
		If MsSeek(xFilial("SE1")+cChave)
			While !Eof() .AND. xFilial("SE1")==SE1->E1_FILIAL .AND. cChave == SE1->(E1_PREFIXO+E1_NUM)
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³Se a origem for pelo financeiro, nao efetua a exclusao do titulo    ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If "FIN"$SE1->E1_ORIGEM
					SE1->(DbSkip())
					Loop
				Endif
				
				Reclock("SE1")
				dbDelete()
				MSUnlock()
				dbSkip()
			End
		EndIf
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Verifica se existe amarracao com lay-away e faz a reabertura se necessario³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc$"EUA|POR"
		dbSelectArea("SL2")
		dbSetOrder(1)
		MsSeek( xFilial("SL2")+SL1->L1_NUM )
		
		dbSelectArea("SLP")
		dbSetOrder(4)
		
		While xFilial("SL2")+SL1->L1_NUM == SL2->L2_FILIAL+SL2->L2_NUM
			If MsSeek(xFilial("SLP")+SL2->L2_NUM+SL2->L2_ITEM)
				a800AbreLW(SL2->L2_NUM,SL2->L2_ITEM)
			EndIf
			SL2->(dbSkip())
		End
	EndIf
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Gerar Nota de Credito ao Cliente - Loc. Guatemala                         ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc == "GUA" .AND. lGeraNCC
		For nI := 1 to Len(aItensSF2)
			Lj140GeraNCC(aItensSF2,aItensSD2,nI)
		Next nI
	EndIf
	
	dbSelectArea( "SL2" )
	dbSetOrder(1)
	MsSeek( xFilial("SL2")+SL1->L1_NUM )
	nContador := 0
	While !Eof( ) .AND. xFilial("SL2") == SL2->L2_FILIAL .AND. SL2->L2_NUM == SL1->L1_NUM
		
		nContador++
		
		dbSelectArea("SL2")
		Reclock( "SL2",.F.,.T.)
		Replace L2_DOC     with ""
		Replace L2_SERIE   With ""
		Replace L2_PDV     With ""
		Replace L2_VENDIDO with " "
		If SL1->L1_JUROS > 0
			Replace L2_VLRITEM With (((L2_PRCTAB * L2_QUANT) - L2_VALDESC) - L2_DESCPRO)
			Replace L2_VRUNIT  With (L2_VLRITEM/L2_QUANT)
		EndIf
		dbSkip()
	End
	Reclock( "SL1" ,.F.,.T.)
	
	Replace L1_DOC      with ""
	Replace L1_SERIE    with ""
	Replace L1_PDV      with ""
	Replace L1_EMISNF   with Ctod(" ")
	Replace L1_TIPO     with ""
	Replace L1_VALBRUT  with SL1->L1_VLRTOT
	Replace L1_VALMERC  with SL1->L1_VLRLIQ
	Replace L1_DINHEIRO with 0
	Replace L1_CHEQUES  with 0
	Replace L1_CARTAO   with 0
	Replace L1_CONVENI  with 0
	Replace L1_VALES    with 0
	Replace L1_FINANC   with 0
	Replace L1_OUTROS   with 0
	Replace L1_VLRDEBI  with 0
	Replace L1_VENDTEF  with ""
	Replace L1_NUMCFIS  with ""
	Replace L1_OPERADO  with ""
	Replace L1_BLCRED   with ""
	If lCrdxInt .AND. SL1->(FieldPos("L1_CONTRA")) > 0
		Replace L1_CONTRA  with ""
	Endif
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//   Zera os campos de troco...                                                                             |
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If cPaisLoc <> "BRA"
		For nI := 1 To MoedFin()
			cCpoTroco := "L1_TROCO"+AllTrim(Str(nI))
			If SL1->(FieldPos(cCpoTroco)) > 0
				SL1->(FieldPut(FieldPos(cCpoTroco),0))
			EndIf
		Next nI
	EndIf
	MsUnlock()
	
	SEF->(DbSetOrder(1))
	SE1->(DbSetOrder(1))
	dbCommitAll()
	
	If ExistBlock("LOJA140")
		ExecBlock("LOJA140",.F.,.F.)
	EndIf
EndIf
Return .T.


/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Exc ³ Autor ³ Mario Angelo          ³ Data ³ 04.04.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de exclusao de Orcamentos                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140ExcOrc(ExpC1)                                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 - Filial utilizada para exclusao (reservas)          ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³  DATA    ³ BOPS ³Program.³ MOTIVO DA ALTERACAO                        ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³03/08/05  ³083747³Geronimo³Melhoria p/ na exclusao de venda com reserva³±±
±±³          ³      ³        ³excluir a venda mas nao excluir orcamento   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140ExcOrc( cFilRes )
Local cFilL2    := SL2->( xFilial( "SL2" ) )
Local cFilE1    := SE1->( xFilial( "SE1" ) )
Local cFilL4    := SL4->( xFilial( "SL4" ) )
Local cParcela  := IIf(GetMV("MV_1DUP")=="1","1","A")
Local aRetCrd   := {}                       //Array  de retorno do cancelamento do contrato - integracao SIGACRD
Local lCrdxInt  := CrdxInt()                //Controla se tem integracao com SIGACRD

Default cFilRes := cFilAnt

If !Empty( cFilL2 )
	cFilL2 := cFilRes
EndIf
If !Empty( cFilE1 )
	cFilE1 := cFilRes
EndIf
If !Empty( cFilL4 )
	cFilL4 := cFilRes
EndIf

dbSelectArea( "SL2" )
If MsSeek( cFilL2+SL1->L1_NUM )
	While !Eof() .AND. cFilL2==L2_FILIAL .AND. L2_NUM == SL1->L1_NUM
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Excluo o orcamento quando se trata de uma venda com reserva                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		IF Empty( SL1->L1_DOCPED)
			Reclock( "SL2" ,.F.)
			dbDelete()
			MsUnlock()
		Else
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Nao excluo o orcamento quando se trata de uma venda com reserva, apenas limpo os campos da reserva ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			Reclock( "SL2" ,.F.)
			SL2->L2_FILRES		:= SPACE(TamSx3("L2_FILRES")[1])
			SL2->L2_ORCRES		:= SPACE(TamSx3("L2_ORCRES")[1])
			MsUnlock()
		EndIf
		dbSkip()
	End
EndIf

dbSelectArea("SE1")
dbSetOrder(1)
If MsSeek(cFilE1+Space(3)+SL1->L1_NUM+cParcela+Substr(MVPROVIS,1,3))
	While !Eof() .AND. cFilE1==E1_FILIAL .AND. E1_NUM == SL1->L1_NUM .AND. E1_TIPO == Substr(MVPROVIS,1,3)
		Reclock("SE1",.F.)
		dbDelete()
		MsUnlock()
		dbSkip()
	End
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//|     Se usar os modulos de gestao de concessionarias chama a funcao que grava os relacionamentos.        |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If GetMv("MV_VEICULO") == "S"
	If !Empty(SL1->L1_VEICTIP) .AND. !Empty(SL1->L1_VEIPESQ)
		FG_DEVLOJA(SL1->L1_VEICTIP,SL1->L1_VEIPESQ,"","")
	EndIf
EndIf

If lCrdxInt .AND. SL1->(FieldPos("L1_CONTRA")) > 0
	If !Empty(SL1->L1_CONTRA)
		aRetCrd      := aClone(CrdxVenda( "3"  ,{"",""}  ,SL1->L1_CONTRA  ,.F.  ,;
		NIL  ,NIL ))
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Nao excluo o orcamento quando se trata de uma venda com reserva                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF Empty( SL1->L1_DOCPED)
	dbSelectArea( "SL4" )
	If MsSeek( cFilL4+SL1->L1_NUM )
		While !Eof() .AND. cFilL4==L4_FILIAL .AND. L4_NUM == SL1->L1_NUM
			Reclock("SL4",.F.)
			dbDelete()
			MsUnlock()
			dbSkip()
		End
	EndIf
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Nao excluo o orcamento quando se trata de uma venda com reserva                           ³
//³ Apenas retorno seus campos ao conteudo original para permitir a reutilizacao do orcamento ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea( "SL1" )
IF Empty( SL1->L1_DOCPED)
	Reclock( "SL1" ,.F.)
	dbDelete()
	MsUnlock()
Else
	Reclock( "SL1" ,.F.)
	SL1->L1_PDV		:= SPACE(TamSx3("L1_PDV")[1])
	SL1->L1_EMISNF	:= CTOD("")
	SL1->L1_TIPO	:= SPACE(TamSx3("L1_TIPO")[1])
	SL1->L1_OPERADO	:= SPACE(TamSx3("L1_OPERADO")[1])
	SL1->L1_DOCPED	:= SPACE(TamSx3("L1_DOCPED")[1])
	SL1->L1_SERPED	:= SPACE(TamSx3("L1_SERPED")[1])
	MsUnlock()
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Inclusao de chamada de Ponto de Entrada - Template.                       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If ExistTemplate("LOJA140B")
	ExecTemplate("LOJA140B",.F.,.F.)
EndIf

If ExistBlock("LOJA140B")
	ExecBlock("LOJA140B",.F.,.F.)
EndIf

Return .T.

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³lj140chkresºAutor ³Andre Alves Veiga   º Data ³  09/04/01   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³No cancelamento da venda verifica se ha reservas em aberto  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ AP6                                                        º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Function lj140chkres(lVendido)
Local nX
Local aReservas 	:= {}
Local nPosProduto 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_PRODUTO"})
Local nPosDescPro 	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_DESCRI"})
Local nPosLocal  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_LOCAL"})
Local nPosReserva  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_RESERVA"})
Local nPosLojaRes  	:= aScan(aHeader,{|x| Alltrim(Upper(x[2]))=="L2_LOJARES"})
Local cLojaLocal	:= ""
Local cLojaReserva	:= ""
Local lRet	 		:= .T.
Local cTexto 		:= If(lVendido,STR0032,STR0033) //"do orçamento"###"da venda"
Local nRegs         := Len(aCols)
Local cOrcPai       := ""
Local nCntRes       := 0
Local lLjPrdRes		:= ExistBlock("LJPRDRES")
Local lLjPrdResT	:= ExistTemplate("LJPRDRES")
Local aLjPrdRes		:= {}  	// Retorno do P.E. LJPRDRES


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Ponto de entrada para tratamento do código e descricao do produto             ³
//³ Retorno a posicao da aCols que contem o codigo e a descricao do produto       ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If lLjPrdResT
	aLjPrdRes := ExecTemplate("LJPRDRES",.F.,.F.,{nPosProduto,nPosDescPro} )
	If ValType(aLjPrdRes) == "A" .AND. Len(aLjPrdRes) >= 2
		nPosProduto	:= aLjPrdRes[1]
		nPosDescPro	:= aLjPrdRes[2]
	Endif
Endif

If lLjPrdRes
	aLjPrdRes := ExecBlock("LJPRDRES",.F.,.F.,{nPosProduto,nPosDescPro} )
	If ValType(aLjPrdRes) == "A" .AND. Len(aLjPrdRes) >= 2
		nPosProduto	:= aLjPrdRes[1]
		nPosDescPro	:= aLjPrdRes[2]
	Endif
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Pega o codigo da loja local               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea("SLJ")
dbSetOrder(3)
If MsSeek(xFilial("SLJ")+SM0->M0_CODIGO+SM0->M0_CODFIL)
	cLojaLocal := SLJ->LJ_CODIGO
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Verifica se o foi feito Reserva para este orcamento, e se o mesmo veio pela venda assistida ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

If !Empty( SL1->L1_DOCPED )
	
	cOrcPai := SL1->L1_NUM
	SL1->( dbSetOrder( 1 ) )
	
	For nX := 1 to nRegs
		If !Empty( GDFieldGet("L2_ORCRES",nX) )
			nCntRes := aScan(aReservas,{|ExpA1| ExpA1[1]+ExpA1[2] == GDFieldGet("L2_FILRES",nX) + GDFieldGet("L2_ORCRES",nX)})
			If nCntRes == 0
				AAdd( aReservas, {GDFieldGet("L2_FILRES",nX), GDFieldGet("L2_ORCRES",nX)} )
				nCntRes := Len( aReservas )
			EndIf
		EndIf
		
		SL1->( MsSeek( aReservas[nCntRes][1]+aReservas[nCntRes][2] )) //Filial da reserva + orcamento da reserva
		If SL1->L1_TIPO == "V"
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|"Este pedido possui orcamentos finalizados, favor efetuar a devolucao do pedido pela rotina de trocas (LOJA020)" |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			MsgStop( STR0038, STR0006)
			SL1->( MsSeek( xFilial("SL1")+cOrcPai ))
			Return .F.
		EndIf
	Next nX
	
	For nX := 1 to Len( aReservas )
		If SL1->( MsSeek( aReservas[nX][1]+aReservas[nX][2] ))
			lj140ExcOrc( aReservas[nX][1] )
		EndIf
	Next nX
	
	SL1->(MsSeek( xFilial( "SL1" ) + cOrcPai ))
Else
	For nX := 1 to nRegs
		If !Empty(aCols[nX][nPosReserva])
			cLojaReserva := Iif(Empty(aCols[nX][nPosLojaRes]),cLojaLocal,aCols[nX][nPosLojaRes])
			aAdd(aReservas, {aCols[nX][nPosReserva],cLojaReserva,aCols[nX][nPosProduto],aCols[nX][nPosLocal]} )
		Endif
	Next nX
	
	If !Empty(aReservas)
		If !(LjCancRes(aReservas))
			lRet := MsgYesNo(STR0034 + cTexto + "?") //"Não foi possível cancelar a reserva. Deseja continuar a exclusão "
		Endif
	Endif
EndIf



Return lRet

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140Anul³ Autor ³ Fernando Machima      ³ Data ³ 03.12.02 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de anulacao de notas fiscais                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140Anul(ExpC1,ExpN1,ExpN2)                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpC1 = Alias do arquivo                                   ³±±
±±³          ³ ExpN1 = Numero do registro                                 ³±±
±±³          ³ ExpN2 = Opcao selecionada                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function lj140Anul(cAlias,nReg,nOpcx)

Lj140Exc(cAlias,nReg,nOpcx)

Return .T.

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Funcao de legenda da tela											   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Function Lj140Leg
Local aLegenda := { {"BR_VERDE",	STR0039},; //"Orçamentos em aberto"
{"BR_VERMELHO",	STR0040},; //"Vendas efetuadas"
{"BR_AMARELO",	STR0041},; //"Orcamentos com reservas"
{"BR_AZUL",		STR0042},; //"Pedidos encerrados"
{"BR_PRETO",	STR0043},; //"Orcamentos em aberto vencidos"
{"BR_MARROM",	STR0044} } //"Devoluções pendentes"
BrwLegenda(STR0045,STR0046,aLegenda) //"Orcamentos"###"Legenda"
Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³ lj140ICOrc³ Autor ³ Adrianne Furtado      ³ Data ³ 05.03.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Programa de impressao e cancelamento de orçamentos		   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ lj140ICOrc(ExpA1)                                           ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = array de registros do orcamento                     ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140ICOrc(aCols)
Local cCodProd
Local cDescriProd
Local cQuant
Local cVrUnit
Local cDesconto
Local cVlrItem
Local cSitTrib
Local cFormaPgtos
Local cNomeForma
Local cMensagem
Local nAliquota
Local nRet
Local nX
Local cTpSolCf := GetMV("MV_TPSOLCF")                  //Parametro para validacao do tipo de cliente para o calculo do solidario
Local cUnidMed := " "

MsgStop(STR0050 + Chr(10)+ Chr(13) + ; 		//"Esse cupom sera cancelado de forma "
STR0051 + Chr(10)+ Chr(13) + ; 		//"automatica. Conforme lei estadual  "
STR0052, STR0047)					//"vigente que regulamenta pre-venda. " // Atencao

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Abre o cupom fiscal                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nRet :=IFAbreCup( nHdlEcf )

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Imprime os itens                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
if nRet == 0
	dbSelectArea("SL2")
	dbSetOrder(1)
	MsSeek(xFilial("SL2")+SL1->L1_NUM)
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Varre o aCols para imprimir os itens                          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX = 1 to Len (aCols)
		
		SB1->(MsSeek(xFilial("SB1")+SL2->L2_PRODUTO))
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³Posiciona o SF4   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		SF4->(MsSeek(xFilial("SF4")+SL2->L2_TES))
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica a situacao tributaria do item                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If SF4->F4_ISS == "S"					// Servico
			cSitTrib := "S" + AllTrim (Str( Iif(SB1->B1_ALIQISS>0,SB1->B1_ALIQISS,GetMv("MV_ALIQISS")),5,2 ))
			
		ElseIf SB1->B1_PICMRET > 0 .AND. SA1->A1_TIPO $ cTpSolCf
			cSitTrib := "F"						// Substituicao tributaria (Icms Solidario)
			
		ElseIf SF4->F4_BASEICM > 0 .AND. SF4->F4_BASEICM < 100
			cSitTrib := "T" + Alltrim(Str( SB0->B0_ALIQRED,5,2 ))		  // com reducao de Icms na Base
			
		ElseIf SF4->F4_LFICM == "I"			// Isento
			cSitTrib := "I"
			
		ElseIf SF4->F4_LFICM == "N"			// N„o sujeito a ICMS
			cSitTrib := "N"
			
		Else									// Com ICMS
			nAliquota := AliqIcms("N","S",SA1->A1_TIPO,"I")
			cSitTrib := "T" + Alltrim(Str(nAliquota,5,2))
			
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Trata as variaveis para o registro do item                   ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		cCodProd 		:= aCols[nX][gdFieldPos("L2_PRODUTO")]
		cDescriProd		:= SubStr(aCols[nX][gdFieldPos("L2_DESCRI")] + Space(30),1,30)
		cQuant 			:= StrZero(aCols[nX][gdFieldPos("L2_QUANT")],7,2)
		cVrUnit			:= Str(aCols[nX][gdFieldPos("L2_VRUNIT")],10,4)
		cDesconto		:= Str(aCols[nX][gdFieldPos("L2_VALDESC")],8,2)
		cVlrItem		:= Str(Val(cVrUnit) * Val(cQuant),11,2)
		cUnidMed        := SB1->B1_UM
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Imprime o item                                               ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nRet := IFRegItem( nHdlECF, cCodProd, cDescriProd, cQuant, cVrUnit, cDesconto, cSitTrib, cVlrItem, cUnidMed )
		If nRet == 1
			MsgStop(STR0053, STR0047) 			//"Falha em Registrar item" // Atencao
			Return .F.
		Endif
		
		dbSelectArea("SL2")
		dbSkip()
	Next nX
Else
	MsgStop(STR0054, STR0047) 				//"Problemas na abertura do cupom" //Atencao
	Return .F.
EndIf

If SL1->L1_DESCONT > 0
	nRet := IfDescTot( nHdlECF, Alltrim(Str(SL1->L1_DESCONT,10,2)))
EndIf

If nRet == 1
	MsgStop(STR0055, STR0047) 			//"Falha no registro do desconto no total" //Atencao
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Posiciona o SL4   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
SL4->(MsSeek(xFilial("SL4")+SL1->L1_NUM))

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Monta a string cFormaPgtos para enviar para o ECF                ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cFormaPgtos := ''
nTotalPgtos := 0
While (nTotalPgtos < SL1->L1_VLRLIQ) .AND. (SL4->L4_NUM == SL1->L1_NUM)
	If Trim(SL4->l4_FORMA) == ''
		cNomeForma := Alltrim(Tabela("24",GetMV("MV_SIMB1"),.F.))
	ElseIf Trim(SL4->L4_FORMA) == 'R$'
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	ElseIf Trim(SL4->L4_FORMA) == 'CH'
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	ElseIf GetMv("MV_LJPAGTO") == 1									//Busca da Tabela 24 do SX5
		cNomeForma := Alltrim(Tabela("24",AllTrim(SL4->L4_FORMA),.F.))
		
	Else
		cNomeForma := Trim(Substr(SL4->L4_ADMINIS,7,14))
		
	EndIf
	
	cFormaPgtos += Trim(cNomeForma) + "|" + Alltrim(Str(SL4->L4_VALOR)) + "|"
	nTotalPgtos += SL4->L4_VALOR
	SL4->(dbSkip())
End Do

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³Envia as formas de pagamento para o ECF                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nRet := IFPagto( nHdlECF, cFormaPgtos  )
If nRet == 1
	MsgStop(STR0056, STR0047) 						//"Falha no registro de pagamento"
	Return .F.
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Fecha o cupom e imprime a mensagem promocional  ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
cMensagem := STR0011  + SL1->L1_NUM + Chr(10)+ Chr(13) + ; 	//"Or‡amento:"
STR0050 + Chr(10)+ Chr(13) + ; 					//"Esse cupom sera cancelado de forma "
STR0051 + Chr(10)+ Chr(13) + ; 					//"automatica. Conforme lei estadual  "
STR0052											//"vigente que regulamenta pre-venda. "

nRet := IFFechaCup( nHdlECF, cMensagem )
If nRet == 1
	MsgStop(STR0057, STR0047) 						//"Falha no fechamento do cupom" // Atencao
	Return .F.
EndIf

nRet := IfCancCup(nHdlECF)
If nRet == 1
	MsgStop(STR0058, STR0047) 	 					//"Falha no cancelamento do cupom"
	Return .F.
EndIf

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140GeraNC³ Autor ³ Fernando Machima      ³ Data ³ 03.05.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Gerar Nota de Credito para o cancelamento - Loc. Guatemala  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140GeraNCC(ExpA1,ExpA2,ExpN1)                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³ ExpA1 = array com os dados da NF cancelada                  ³±±
±±³Parametros³ ExpA2 = array com os dados dos itens da NF cancelada        ³±±
±±³Parametros³ ExpN1 = iteracao                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function Lj140GeraNCC(aItensSF2,aItensSD2,nI)

Local cEstado  := ""
Local cItemOri := ""
Local cItem    := ""
Local cCampo   := ""
Local cTESNCC  := ""
Local nX, nY
Local nItem    := 0
Local nTamItem := TamSX3("D1_ITEM")[1]
Local nTotItens:= 0
Local nPosCols := 1
Local nPosRow  := 0
Local nPosCpo  := 0
Local nValImps := 0
Local aTesImpInf  := {}
Local aImps    := {}
Local aCustoDev:= {}
Local aGetBook := {}
Local aLivro   := {}
Local aImpVarSF1 := {}

Private aImpVarSD1 := {0,0,0,0,0,{}}

SA1->(DbSetOrder(1))
If SA1->(MsSeek(xFilial("SA1")+aItensSF2[nI][3]+aItensSF2[nI][4]))
	cEstado  := SA1->A1_EST
EndIf

RecLock("SF1",.T.)
Replace F1_FILIAL	With xFilial("SF1")
Replace F1_DOC   	With cNumNFDev
Replace F1_SERIE	With cSerieDev
Replace F1_FORNECE 	With aItensSF2[nI][3]
Replace F1_LOJA	 	With aItensSF2[nI][4]
Replace F1_EMISSAO 	With dDataBase
Replace F1_DTDIGIT 	With dDataBase
Replace F1_TIPO	 	With "D"
Replace F1_ESPECIE 	With "NCC"
Replace F1_EST 	 	With cEstado
Replace F1_FORMUL  	With "S"
Replace F1_MOEDA  	With aItensSF2[nI][7]
Replace F1_TXMOEDA 	With aItensSF2[nI][8]
Replace F1_TIPODOC 	With "04"
Replace F1_VALMERC 	With aItensSF2[nI][6]
Replace F1_VALBRUT 	With aItensSF2[nI][5]
If cPaisLoc == "GUA"
	Replace F1_MOTIVO 	With cMotivo
	If FieldPos("F1_MANUAL") > 0
		Replace F1_MANUAL With Iif(lNFManual,"S","N")
	EndIf
EndIf

For nX := 1 to Len(aItensSD2)
	nTotItens  += aItensSD2[nX][7]
Next nX

nItem  := 1
While aItensSD2[nItem][15] == aItensSF2[nI][1] .AND. ;
	aItensSD2[nItem][16] == aItensSF2[nI][2]
	
	cItem  :=  StrZero(nItem,nTamItem)
	
	SB1->(DbSetOrder(1))
	SB1->(MsSeek(xFilial("SB1")+aItensSD2[nItem][2]))
	
	SF4->(DbSetOrder(1))
	SF4->(MsSeek(xFilial("SF4")+aItensSD2[nItem][9]))
	cTESNCC  := IIf(!Empty(SF4->F4_TESNCC),SF4->F4_TESNCC,GetMV("MV_TESTROC"))
	SF4->(MsSeek(xFilial("SF4")+cTESNCC))
	
	aCustoDev := {aItensSD2[nItem][17]/aItensSD2[nItem][5],;
	aItensSD2[nItem][18]/aItensSD2[nItem][5],;
	aItensSD2[nItem][19]/aItensSD2[nItem][5],;
	aItensSD2[nItem][20]/aItensSD2[nItem][5],;
	aItensSD2[nItem][21]/aItensSD2[nItem][5]}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcular os impostos variaveis                              ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aImpVarSD1 := {0,0,0,0,0,{}}
	aImpVarSD1[1] := aItensSD2[nItem][5]
	aImpVarSD1[2] := aItensSD2[nItem][6]
	aImpVarSD1[3] := aItensSD2[nItem][7]
	nPosRow  := nItem
	CalcTesxIp("E",nTotItens,aItensSD2[nItem][14],aItensSD2[nItem][2],nPosCols,nPosRow,"RAPIDA")
	
	Reclock("SD1",.T.)
	Replace D1_FILIAL  	With xFilial("SD1")
	Replace D1_COD 	 	With aItensSD2[nItem][2]
	Replace D1_UM		With aItensSD2[nItem][3]
	Replace D1_SEGUM		With aItensSD2[nItem][4]
	Replace D1_GRUPO	    With aItensSD2[nItem][13]
	Replace D1_TIPO	 	With "D"
	Replace D1_ORIGLAN	With "LO"
	Replace D1_TP		With SB1->B1_TIPO
	Replace D1_NUMSEQ  	With ProxNum()
	Replace D1_QUANT	    With aItensSD2[nItem][5]
	Replace D1_VUNIT	    With aItensSD2[nItem][6]
	Replace D1_TOTAL	    With aItensSD2[nItem][7]
	Replace D1_TES 	 	With cTESNCC
	Replace D1_FORNECE 	With aItensSD2[nItem][11]
	Replace D1_LOJA	 	With aItensSD2[nItem][12]
	Replace D1_DOC 	 	With cNumNFDev
	Replace D1_SERIE	    With cSerieDev
	Replace D1_EMISSAO 	With dDataBase
	Replace D1_DTDIGIT 	With dDataBase
	Replace D1_NFORI	    With aItensSD2[nItem][15]
	Replace D1_SERIORI 	With aItensSD2[nItem][16]
	Replace D1_ITEMORI   With aItensSD2[nItem][1]
	Replace D1_NUMCQ   	With xNumCaixa()
	Replace D1_LOCAL	    With aItensSD2[nItem][8]
	Replace D1_ITEM	 	With cItem
	Replace D1_CF		With aItensSD2[nItem][10]
	Replace D1_ESPECIE   With "NCC"
	Replace D1_FORMUL  	With "S"
	Replace D1_TIPODOC 	With "04"
	Replace D1_CUSTO     With aCustoDev[1]
	Replace D1_CUSTO2    With aCustoDev[2]
	Replace D1_CUSTO3    With aCustoDev[3]
	Replace D1_CUSTO4    With aCustoDev[4]
	Replace D1_CUSTO5    With aCustoDev[5]
	
	//   Replace D1_DESC    	With aCols[nC][nPosDesc]
	//   Replace D1_VALDESC 	With aCols[nC][nPosValDesc]
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gravar os impostos variaveis no detalhe da Nota de Credito  ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len(aImpVarSD1[6])
		If Substr(aImpVarSD1[6,nX,7],3,7) == "_BASIMP"
			cCampo   := "D1"+Substr(aImpVarSD1[6,nX,7],3,8)
			nPosCpo  := FieldPos(cCampo)
			FieldPut(nPosCpo,aImpVarSD1[6,nX,3])
		EndIf
		
		If Substr(aImpVarSD1[6,nX,6],3,7) == "_VALIMP"
			cCampo   := "D1"+Substr(aImpVarSD1[6,nX,6],3,8)
			nPosCpo  := FieldPos(cCampo)
			FieldPut(nPosCpo,aImpVarSD1[6,nX,4])
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//|    Soma os impostos discriminados                                                                       |
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			If Substr(aImpVarSD1[6,nX,5],2,1) == "1"
				nValImps  += aImpVarSD1[6,nX,4]
			EndIf
		EndIf
	Next nX
	MsUnlock()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gerar os impostos variaveis para gravar no cabecalho da NCC ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	For nX := 1 To Len( aImpVarSD1[6] )
		If (nY := aScan( aImpVarSF1,{|x| x[1] == aImpVarSD1[6,nX,8] } )) == 0
			AAdd( aImpVarSF1, { aImpVarSD1[6,nX,8], 0.00, aImpVarSD1[6,nX,2], aImpVarSD1[6,nX,1] } )
			nY := Len( aImpVarSF1 )
		EndIf
		aImpVarSF1[ nY,2 ] += aImpVarSD1[6,nX,4]
		
		If (nY := aScan( aImpVarSF1,{|x| x[1] == aImpVarSD1[6,nX,9] } )) == 0
			AAdd( aImpVarSF1, { aImpVarSD1[6,nX,9], 0.00, aImpVarSD1[6,nX,2], aImpVarSD1[6,nX,1] } )
			nY := Len( aImpVarSF1 )
		EndIf
		aImpVarSF1[ nY,2 ] += aImpVarSD1[6,nX,3]
	Next nX
	
	SA1->(DbSetOrder(1))
	SA1->(MsSeek(xFilial("SA1")+SF1->F1_FORNECE+SF1->F1_LOJA))
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Gerar o registro do Livro Fiscal                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	aLivro := GetBook(@aGetBook, aImpVarSD1, "V", aItensSF2[nI][8], aLivro, "E" )
	
	nItem++
	If nItem > Len(aItensSD2)
		Exit
	EndIf
End

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravar os impostos variaveis no cabecalho da Nota de Credito³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 1 To Len(aImpVarSF1)
	aImpVarSF1[nX,1] := "F1"+Substr(aImpVarSF1[nX,1],3,8)
Next nX
RecLock("SF1",.F.)
For nX := 1 To Len(aImpVarSF1)
	If (aImpVarSF1[nX,2] > 0)
		nPosCpo := FieldGet(FieldPos(aImpVarSF1[nX,1]))+aImpVarSF1[nX,2]
		SF1->( FieldPut( FieldPos( aImpVarSF1[nX,1] ),nPosCpo ) )
	EndIf
Next nX
SF1->F1_VALBRUT  := nTotItens + nValImps
MsUnLock()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Gravar Registro nos Livros Fiscais...			           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
For nX := 2 To Len( aLivro )
	RecLock( "SF3",.T. )
	For nY := 1 To Len( aLivro[1] )
		SF3->( FieldPut(FieldPos(aLivro[1,nY]),aLivro[nX,nY]) )
	Next nY
	Replace F3_FILIAL  With xFilial("SF3")
	Replace F3_FORMUL  With "S"
	Replace F3_ESPECIE With "NCC"
	Replace F3_TIPO    With "D"
	Replace F3_CLIEFOR With aItensSF2[nI][3]
	Replace F3_LOJA	  With aItensSF2[nI][4]
	MsUnLock()
Next nX

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡„o    ³Lj140Manual³ Autor ³ Fernando Machima      ³ Data ³ 22.06.04 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡„o ³ Pede confirmacao para NCC Manual                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ Lj140Manual()                                               ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Uso       ³LOJA140                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Function Lj140Manual()

Local lRet   := .T.
Local nOpcao := 1

If cPaisLoc == "GUA" .AND. lNFManual
	nOpcao := Aviso(STR0006,STR0073,{STR0074,STR0075}) //"Aten‡„o"###"Sera gravada uma Nota de Credito MANUAL. Confirma a operacao?"###"Confirma"###"Cancela"
	If nOpcao == 2
		lRet  := .F.
	EndIf
EndIf

Return (lRet)
