#INCLUDE "MATR440.CH"
#INCLUDE "PROTHEUS.CH"

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ MATR440  ³ Autor ³ Eveli Morasco         ³ Data ³ 16/04/93 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Lista os itens que atingiram o ponto de pedido             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ Generico                                                   ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³         ATUALIZACOES SOFRIDAS DESDE A CONSTRU€AO INICIAL.             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Programador ³ Data   ³ BOPS ³  Motivo da Alteracao                     ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Marcelo P.S.³17/11/97³12601A³ Incluir pergunta:para considerar C.Q.    ³±±
±±³Rogerio F.G.³02/12/97³13690A³ Ajuste Utiliza. Cpo B1_QE,B1_LM          ³±±
±±³Marcelo P.  ³13/02/98³xxxxxx³ Ajuste no Campo B1_QE.                   ³±±
±±³Rodrigo     ³19/02/98³11231A³ Ajuste no Calculo da necessida qdo usa   ³±±
±±³            ³        ³      ³ Ponto de Pedido (B1_EMIN)                ³±±
±±³Eduardo     ³21.05.98³16326A³ Acerto para considerar Estoque de Seg.   ³±±
±±³Rodrigo Sart³11/09/98³6742A ³ Ajuste Utiliza. Cpo B1_QE,B1_LM          ³±±
±±³Rodrigo Sart³05/11/98³XXXXXX³ Acerto p/ Bug Ano 2000                   ³±±
±±³Edson       ³25.11.98³18720 ³ Correcao no calculo do saldo por almox.  ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
User Function FMatr440
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Define Variaveis                                             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LOCAL wnrel
LOCAL Tamanho  := "G"
LOCAL cDesc1   := STR0001	//"Emite uma relacao com os itens em estoque que atingiram o Ponto de"
LOCAL cDesc2   := STR0002	//"Pedido ,sugerimdo a quantidade a comprar."
LOCAL cDesc3   := ""

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis tipo Private padrao de todos os relatorios         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE nomeprog := "MATR440"
PRIVATE cString  := "SB1"
PRIVATE aReturn := { OemToAnsi(STR0003), 1,OemToAnsi(STR0004), 1, 2, 1, "",1 }		//"Zebrado"###"Administracao"
PRIVATE nLastKey := 0 ,cPerg := "MTR440"
PRIVATE titulo   := "Itens em Ponto de Pedido - Filial "+SM0->M0_NOME
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Contadores de linha e pagina                                 ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
PRIVATE li := 80 ,m_pag := 1

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Funcao utilizada para verificar a ultima versao dos fontes      ³
//³ SIGACUS.PRW, SIGACUSA.PRX e SIGACUSB.PRX, aplicados no rpo do   |
//| cliente, assim verificando a necessidade de uma atualizacao     |
//| nestes fontes. NAO REMOVER !!!							        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF !(FindFunction("SIGACUS_V") .and. SIGACUS_V() >= 20050512)
	Aviso("Atencao","Atualizar patch do programa SIGACUS.PRW !!!",{"Ok"})
	Return
EndIf
IF !(FindFunction("SIGACUSA_V") .and. SIGACUSA_V() >= 20050512)
	Aviso("Atencao","Atualizar patch do programa SIGACUSA.PRX !!!",{"Ok"})
	Return
EndIf
IF !(FindFunction("SIGACUSB_V") .and. SIGACUSB_V() >= 20050512)
	Aviso("Atencao","Atualizar patch do programa SIGACUSB.PRX !!!",{"Ok"})
	Return
EndIf
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica as perguntas selecionadas                           ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                         ³
//³ mv_par01             // Produto de                           ³
//³ mv_par02             // Produto ate                          ³
//³ mv_par03             // Grupo de                             ³
//³ mv_par04             // Grupo ate                            ³
//³ mv_par05             // Tipo de                              ³
//³ mv_par06             // Tipo ate                             ³
//³ mv_par07             // Local de                             ³
//³ mv_par08             // Local ate                            ³
//³ mv_par09             // Considera Necess Bruta   1 - Sim     ³ Pto Pedido
//³ mv_par10             // Saldo Neg Considera      1 - Sim     ³ Lote Economico
//³ mv_par11             // Considera C.Q.           1 - Sim     ³
//³ mv_par12             // Cons.Qtd. De 3os.? Sim / Nao         ³
//³ mv_par13             // Cons.Qtd. Em 3os.? Sim / Nao         ³
//³ mv_par14             // Qtd. PV nao Liberado ?" Subtr/Ignora ³
//³ mv_par15             // Descricao completa do produto?       ³
//³ mv_par16             // Considera Saldo Armazem de           ³
//³ mv_par17             // Considera Saldo Armazem ate          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
AjustaSX1()
pergunte(cPerg,.F.)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Envia controle para a funcao SETPRINT                        ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
wnrel:=SetPrint(cString,NomeProg,cPerg,@titulo,cDesc1,cDesc2,cDesc3,.F.,"",.F.,Tamanho)

If nLastKey = 27
	dbClearFilter()
	Return
Endif

SetDefault(aReturn,cString)

If nLastKey = 27
	dbClearFilter()
	Return
Endif

RptStatus({|lEnd| FR440Imp(@lEnd,tamanho,wnrel,cString)},Titulo)

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ R440IMP  ³ Autor ³ Cristina M. Ogura     ³ Data ³ 09.11.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Chamada do Relatorio                                       ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ MATR440			                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function FR440Imp(lEnd,tamanho,wnrel,cString)
LOCAL nQuant := nSaldo := nSaldoMatriz := 0 ,nValUnit ,nValor ,nValTot ,cTipoVal ,nPrazo
LOCAL nToler, nEstSeg, nNeces := 0
LOCAL nCntImpr := 0
LOCAL cRodaTxt := STR0006	//"PRODUTO(S)"
LOCAL nTipo    := 0
LOCAL cabec1,cabec2
LOCAL cLocCQ	:= GetMV("MV_CQ")
LOCAL nAuxQuant:= 0
LOCAL lMR440QTD:= ExistBlock( "MR440QTD" )
Local nX       := 0
Local nPrevis := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa os codigos de caracter Comprimido/Normal da impressora ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
nTipo  := IIF(aReturn[4]==1,15,18)

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Monta os Cabecalhos                                          ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par15 == 1
	cabec1 := "CODIGO  DESCRICAO               TP GRP  UM  SALDO ATUAL  SLD MATRIZ   ENTRADA     PONTO DE   ESTOQUE DE         LOTE ___TOLERANCIA___   QUANTIDADE QUANTIDADE A   VALOR ESTIMADO BASE  DATA DE   VALOR UNITARIO    PRAZO DE"
	cabec2 := "                                                                      PREVISTA     PEDIDO    SEGURANCA    ECONOMICO   %   QUANTIDADE   POR EMBAL.      COMPRAR        DA COMPRA      REFERENCIA      DA COMPRA     ENTREGA"
	//         123456  123456789012345678901  12 1234 12 9.999.999,99 9.999.999,99 9.999.999,99 9.999.999,99 999 9.999.999,99 9.999.999,99 9.999.999,99 9.999.999.999,99 XXXX 99/99/9999 999.999.999,99 99999 Dia(s)
	//         0         1         2         3         4         5         6         7         8         9        10        11        12        13        14        15        16        17        18        19        20        21
	//         0123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123456789012345678901234567890123
Else
	cabec1 := "CODIGO                          TP GRP  UM  SALDO ATUAL  SLD MATRIZ   ENTRADA     PONTO DE   ESTOQUE DE         LOTE ___TOLERANCIA___   QUANTIDADE QUANTIDADE A   VALOR ESTIMADO BASE  DATA DE   VALOR UNITARIO    PRAZO DE"
	cabec2 := "DESCRICAO                                                             PREVISTA     PEDIDO    SEGURANCA    ECONOMICO   %   QUANTIDADE   POR EMBAL.      COMPRAR        DA COMPRA      REFERENCIA      DA COMPRA     ENTREGA"
Endif	

dbSelectArea("SB1")
SetRegua(RecCount())

Set SoftSeek On
dbSeek( cFilial+mv_par01 )
Set SoftSeek Off
nValTot := 0

While !Eof() .And. B1_FILIAL+B1_COD <= cFilial+mv_par02
	
	If lEnd
		@PROW()+1,001 PSAY STR0009		//"CANCELADO PELO OPERADOR"
		Exit
	Endif
	
	IncRegua()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra grupos e tipos nao selecionados e tambem se for MOD³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If B1_GRUPO < mv_par03 .Or. B1_GRUPO > mv_par04 .Or.;
		B1_TIPO  < mv_par05 .Or. B1_TIPO  > mv_par06 .Or.;
		SubStr(B1_COD,1,3) == "MOD" .Or. B1_TIPO == "BN" .Or.;
		B1_CONTRAT == "S" .Or. B1_CONTRAT == "A"
		dbSkip()
		Loop
	EndIf

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Filtra armazem padão SB1 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If B1_LOCPAD < mv_par07 .Or. B1_LOCPAD > mv_par08
		dbSkip()
		Loop
	EndIf
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula o saldo atual da Matriz Conforme solicitacao de 28/10/05 ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB2")
	dbSeek( "01"+SB1->B1_COD )
	While !Eof() .And. B2_FILIAL+B2_COD == "01"+SB1->B1_COD
		If B2_LOCAL < mv_par16 .OR. B2_LOCAL > mv_par17
			dbSkip()
			Loop
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ inclui os produtos que estao no C.Q.      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If B2_LOCAL == cLocCQ .And. mv_par11 == 2
			dbSkip()
			Loop
		Endif
		nSaldoMatriz += SB2->B2_QATU - SB2->B2_RESERVA
		//nSaldoMatriz += (SaldoSB2(NIL,NIL,NIL,mv_par12==1,mv_par13==1)+B2_SALPEDI+B2_QACLASS)
		If mv_par14 == 1
		   nSaldoMatriz -= SB2->B2_QPEDVEN
		EndIf
		dbSkip()
	EndDo
	//nEstSeg := CalcEstSeg( RetFldProd(SB1->B1_COD,"B1_ESTFOR") )
	//nSaldo -= nEstSeg

	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Direciona para funcao que calcula o necessidade de compra ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Calcula o saldo atual de todos os almoxarifados ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	dbSelectArea("SB2")
	dbSeek( cFilial+SB1->B1_COD )
	While !Eof() .And. B2_FILIAL+B2_COD == cFilial+SB1->B1_COD
		If B2_LOCAL < mv_par16 .OR. B2_LOCAL > mv_par17
			dbSkip()
			Loop
		EndIf
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ inclui os produtos que estao no C.Q.      ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If B2_LOCAL == cLocCQ .And. mv_par11 == 2
			dbSkip()
			Loop
		Endif
		nSaldo += (SaldoSB2(NIL,NIL,NIL,mv_par12==1,mv_par13==1)+B2_SALPEDI+B2_QACLASS)
		If mv_par14 == 1
		   nSaldo -= B2_QPEDVEN
		EndIf
		nPrevis += B2_SALPEDI
		dbSkip()
	EndDo
	nEstSeg := CalcEstSeg( RetFldProd(SB1->B1_COD,"B1_ESTFOR") )
	nSaldo -= nEstSeg
	If (Round(nSaldo,4) # 0) .Or. (mv_par09 == 1)
		Do Case
			Case ( RetFldProd(SB1->B1_COD,"B1_EMIN") != 0 .And. MV_PAR09 == 1 )
				If ( MV_PAR10 == 2 .And. nSaldo < 0 )
					nSaldo -= RetFldProd(SB1->B1_COD,"B1_LE")
				EndIf
				nNeces := If((nSaldo < 0),Abs(nSaldo)+RetFldProd(SB1->B1_COD,"B1_EMIN"),(RetFldProd(SB1->B1_COD,"B1_EMIN")-nSaldo))
			Case ( RetFldProd(SB1->B1_COD,"B1_EMIN") != 0 .And. MV_PAR09 == 2 )
				If ( MV_PAR10 == 2 .And. nSaldo < 0 )
					nSaldo -= RetFldProd(SB1->B1_COD,"B1_LE")
				EndIf
				nNeces := If((nSaldo < 0),Abs(nSaldo),(RetFldProd(SB1->B1_COD,"B1_EMIN")-nSaldo))
			Case ( RetFldProd(SB1->B1_COD,"B1_LE") != 0 .And. (nSaldo < 0  .or. mv_par09 == 2) )
				If ( MV_PAR10 == 2 .And. nSaldo < 0 )
					nNeces := Abs(nSaldo)+RetFldProd(SB1->B1_COD,"B1_LE")
				Else
					nNeces := If( Abs(nSaldo)<RetFldProd(SB1->B1_COD,"B1_LE"),RetFldProd(SB1->B1_COD,"B1_LE"),if(nSaldo<0,Abs(nSaldo),0))
				EndIf
			OtherWise
				nNeces := IF(MV_PAR09 == 1,IIf(nSaldo<0,Abs(nSaldo),0),0)
		EndCase
	Else
		If RetFldProd(SB1->B1_COD,"B1_EMIN") != 0
			nNeces := ( RetFldProd(SB1->B1_COD,"B1_EMIN") )
		Else
			nNeces := 0
		Endif
	EndIf
	
	If nNeces > 0
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica se o produto tem estrutura                       ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		dbSelectArea("SG1")
		If dbSeek( xFilial()+SB1->B1_COD )
			aQtdes := CalcLote(SB1->B1_COD,nNeces,"F")
		Else
			aQtdes := CalcLote(SB1->B1_COD,nNeces,"C")
		Endif
		For nX := 1 to Len(aQtdes)
			nQuant += aQtdes[nX]
		Next
	EndIf
	
	dbSelectArea("SB1")
	
	If lMR440QTD
		nAuxQuant := Execblock("MR440QTD",.f.,.f.,NQUANT)
		if ValType(nAuxQuant) == "N"
			nQuant := nAuxQuant
		EndIf
	EndiF
	
	If nQuant > 0
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Pega o prazo de entrega do material         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nPrazo := CalcPrazo(SB1->B1_COD,nQuant)
		dbSelectArea("SB1")
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Calcula a tolerancia do item                ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nToler   := (RetFldProd(SB1->B1_COD,"B1_LE") * RetFldProd(SB1->B1_COD,"B1_TOLER"))/100
		
		If li > 55
			Cabec(titulo,cabec1,cabec2,nomeprog,Tamanho,nTipo)
		EndIf
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Adiciona 1 ao contador de registros impressos         ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		nCntImpr++
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Verifica qual dos precos e' mais recente servir de base ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If RetFldProd(SB1->B1_COD,"B1_UCOM") < B1_DATREF
			cTipoVal := "STD"
			dData    := B1_DATREF
			nValUnit := RetFldProd(SB1->B1_COD,"B1_CUSTD")
		Else
			cTipoVal := "U.CO"
			dData    := RetFldProd(SB1->B1_COD,"B1_UCOM")
			nValUnit := RetFldProd(SB1->B1_COD,"B1_UPRC")
		EndIf
		nValor := nQuant * nValUnit

		@ li,000 PSAY Substr(Alltrim(B1_COD),1,6)
		If mv_par15 == 1
			@ li,008 PSAY SubStr(B1_DESC,1,20)
		Else
			li++
			@ li,000 PSAY SubStr(B1_DESC,1,30)			
		Endif	
		@ li,032 PSAY B1_TIPO
		@ li,035 PSAY B1_GRUPO
		@ li,040 PSAY B1_UM
		@ li,043 PSAY nSaldo-nPrevis Picture PesqPictQt("B1_LE",12)
		@ li,053 PSAY nSaldoMatriz Picture PesqPictQt("B1_LE",12)
		@ li,064 PSAY nPrevis   Picture PesqPict("SB2","B2_SALPEDI",12)
		@ li,076 PSAY RetFldProd(SB1->B1_COD,"B1_EMIN") Picture PesqPictQt("B1_EMIN",12)
		@ li,088 PSAY nESTSEG   Picture PesqPictQt("B1_ESTSEG",12)
		@ li,101 PSAY RetFldProd(SB1->B1_COD,"B1_LE") Picture PesqPictQt("B1_LE",12)
		@ li,114 PSAY RetFldProd(SB1->B1_COD,"B1_TOLER")  Picture "999"
		@ li,118 PSAY nToler    Picture PesqPictQt("B1_LE",12)
		@ li,131 PSAY RetFldProd(SB1->B1_COD,"B1_QE") Picture PesqPictQt("B1_LE",12)
		@ li,144 PSAY nQuant    Picture PesqPictQt("B1_LE",12)
		@ li,157 PSAY nValor    Picture TM(nValor,16)
		@ li,174 PSAY cTipoVal
		@ li,179 PSAY dData
		@ li,190 PSAY nValUnit  Picture TM(nValUnit,14)
		@ li,205 PSAY nPrazo    Picture "99999"
		@ li,211 PSAY "Dia(s)"

/*
		@ li,000 PSAY B1_COD
		@ li,016 PSAY SubStr(B1_DESC,1,30)
		@ li,047 PSAY B1_TIPO
		@ li,050 PSAY B1_GRUPO
		@ li,055 PSAY B1_UM
		@ li,058 PSAY nSaldo    Picture PesqPictQt("B1_LE",12)
		@ li,071 PSAY B1_EMIN   Picture PesqPictQt("B1_EMIN",12)
		@ li,084 PSAY nESTSEG   Picture PesqPictQt("B1_ESTSEG",12)
		@ li,097 PSAY B1_LE     Picture PesqPictQt("B1_LE",12)
		@ li,110 PSAY B1_TOLER  Picture "999"
		@ li,114 PSAY nToler    Picture PesqPictQt("B1_LE",12)
		@ li,127 PSAY B1_QE     Picture PesqPictQt("B1_LE",12)
		@ li,140 PSAY nQuant    Picture PesqPictQt("B1_LE",12)
		@ li,153 PSAY nValor    Picture TM(nValor,16)
		@ li,170 PSAY cTipoVal
		@ li,175 PSAY dData
		@ li,186 PSAY nValUnit  Picture TM(nValUnit,14)
		@ li,201 PSAY nPrazo    Picture "99999"
		@ li,207 PSAY OemtoAnsi(STR0011)  //  "Dia(s)"
*/
		nValTot += nValor
		li++
		
	Endif
	
	nSaldo := 0
	nQuant := 0
	nPrevis:= 0
	nSaldoMatriz := 0
	
	dbSelectArea("SB1")
	dbSkip()
	
EndDo

If li != 80
	Li++
	@ li,000 PSAY STR0010+Replicate(".",137)		//"TOTAL GERAL A COMPRAR"
	@ li,158 PSAY nValTot Picture TM(nValTot,16)
	Roda(nCntImpr,cRodaTxt,Tamanho)
EndIf

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Devolve a condicao original do arquivo principal             ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
dbSelectArea(cString)
dbClearFilter()
Set Order To 1

If aReturn[5] = 1
	Set Printer To
	dbCommitAll()
	OurSpool(wnrel)
Endif

MS_FLUSH()

Return .T.

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Funcao    ³AjustaSX1 ³ Autor ³ Nereu Humberto Jr     ³ Data ³01.08.2005³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³Cria as perguntas necesarias para o programa                ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Retorno   ³Nenhum                                                      ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³Nenhum                                                      ³±±
±±³          ³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ Programador   ³Manutencao Efetuada                         ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³          ³               ³                                            ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

Local aHelpPor :={ }
Local aHelpEng :={ }
Local aHelpSpa :={ }

PutSX1("MTR440","14","Qtd. PV nao Liberado ?","Ctd. PV no Liberado ?","Qt So Not Relesead ?","mv_che","N",01,0,1,"C","","","","","mv_par14","Subtrae","Resta","Subtract","","Ignora","Ignora","Ignore","","","","","","","","","")
//-------------------------------------------------------------------------------------------------------------------------------//
Aadd( aHelpPor, "Informar se a impressao da descricao do " )
Aadd( aHelpPor, "produto sera reduzida ou completa.      " )

Aadd( aHelpEng, "                                        " )
Aadd( aHelpEng, "                                        " )

Aadd( aHelpSpa, "                                        " ) 
Aadd( aHelpSpa, "                                        " ) 

PutSx1( "MTR440","15","Descricao completa do produto?","Descricao completa do produto?","Descricao completa do produto?","mv_chf",;
"N",1,0,1,"C","","","","","mv_par15","Nao","Nao","Nao","","Sim","Sim","Sim","","","","","","","","","")
PutSX1Help("P.MTR44015.",aHelpPor,aHelpEng,aHelpSpa)
//-------------------------------------------------------------------------------------------------------------------------------//
aHelpPor :={ }
aHelpEng :={ }
aHelpSpa :={ }

aAdd( aHelpPor, "Armazem inicial a ser considerado na    " )
aAdd( aHelpPor, "filtragem do Cadastro de Saldos (SB2).  " )

aAdd( aHelpEng, "To filter stock from initial            " )
aAdd( aHelpEng, "warehouse (SB2).                        " )

aAdd( aHelpSpa, "Filtrar Saldo Deposito inicial (SB2).   " )
aAdd( aHelpSpa, "                                        " )

PutSX1("MTR440","16","Considera Saldo Armazem de", "Consd. Deposito de","Cons. Warehouse from","mv_chg",;
"C",2,0,1,"G","","","","","mv_par16","","","","","","","","","","","","","","","","")
PutSX1Help("P.MTR44016.",aHelpPor,aHelpEng,aHelpSpa)
//-------------------------------------------------------------------------------------------------------------------------------//
aHelpPor :={ }
aHelpEng :={ }
aHelpSpa :={ }

aAdd( aHelpPor, "Armazem final a ser considerado na      " )
aAdd( aHelpPor, "filtragem do Cadastro de Saldos (SB2).  " )

aAdd( aHelpEng, "To filter stock from final              " )
aAdd( aHelpEng, "warehouse (SB2).                        " )

aAdd( aHelpSpa, "Filtrar Saldo Deposito final (SB2).     " )
aAdd( aHelpSpa, "                                        " )

PutSX1("MTR440","17","Considera Saldo Armazem ate","Consd. Deposito a", "Cons. Warehouse to","mv_chh",;
"C",2,0,1,"G","","","","","mv_par17","","","","ZZ","","","","","","","","","","","","")
PutSX1Help("P.MTR44017.",aHelpPor,aHelpEng,aHelpSpa)

Return