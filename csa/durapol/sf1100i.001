/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³SF1100I   ºAutor  ³Reinaldo Caldas     º Data ³  20/04/05   º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³                                                            º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Durapol                                                    º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
USER FUNCTION SF1100I

LOCAL _aArea        := GetArea()
LOCAL _aAreaSD1     := SD1->(GetArea())
LOCAL _aAreaSF4     := SF4->(GetArea())
LOCAL _aAreaSA1     := SA1->(GetArea())
LOCAL _aAreaSA2     := SA2->(GetArea())
LOCAL _aAreaSE4     := SE4->(GetArea())
LOCAL _aAreaSB2     := SB2->(GetArea())
LOCAL _cTSServ      := GetMV("MV_X_TSVED") //Tes para faturamento (venda)
LOCAL _aSC5         := {} 
LOCAL _aSC6         := {}
LOCAL _aLinha       := {}
LOCAL _nValor       := 0
LOCAL i := 0
PRIVATE lMsHelpAuto := .F.
PRIVATE lMsErroAuto := .F.
                   
// Somente executa tratamento para Notas Fiscais de Coleta (Tipo Beneficiamento) da Empresa Durapol (03)
IF FunName() = "MATA103" .and. SF1->F1_TIPO = "B" .and. SM0->M0_CODIGO == "03" 
	
	SA1->(dbSetOrder(1))
	SA1->(dbSeek(xFilial("SA1")+SF1->F1_FORNECE+SF1->F1_LOJA,.f.))
	
	SE4->(dbSetOrder(1))
	SE4->(dbSeek(xFilial("SE4")+SF1->F1_COND,.f.))
	
	SC5->(dbSeek(xFilial("SC5")))
	SC6->(dbSeek(xFilial("SC6")))
	
    //Adiciona Cabecalho do Pedido Venda (SC5) a partir do Cabecalho Nota Fiscal Entrada Coleta (SF1)
	_aSC5	:= 	{{'C5_FILIAL',xFilial("SC5"),NIL},;
				{'C5_NUM',SF1->F1_DOC,NIL},;
				{'C5_TIPO','N',NIL},;
				{'C5_CLIENTE',SF1->F1_FORNECE,NIL},;
				{'C5_LOJACLI',SF1->F1_LOJA,NIL},;
				{'C5_TABELA',"   ",NIL},;
				{'C5_VEND3',SA1->A1_VEND3,NIL},;
				{'C5_COMIS3',SA1->A1_COMIS3,NIL},;
				{'C5_VEND4',SA1->A1_VEND4,NIL},; 
				{'C5_COMIS4',SA1->A1_COMIS4,NIL},;
				{'C5_VEND5',SA1->A1_VEND5,NIL},;
				{'C5_COMIS5',SA1->A1_COMIS5,NIL},;
				{'C5_CONDPAG',iif(Empty(SF1->F1_COND),SA1->A1_COND,SF1->F1_COND),NIL}}

	dbSelectArea("SD1")
	dbSeek(xFilial("SD1")+SF1->(F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA),.F.)
	Do While SD1->(!eof() .and. D1_FILIAL=xFilial("SD1") .and. D1_DOC+D1_SERIE+D1_FORNECE+D1_LOJA=SF1->(F1_DOC+F1_SERIE+F1_FORNECE+F1_LOJA))

		_aLinha := {}
		_nValor := 0
        
		For i:= 1 To SD1->D1_QUANT

			_aLinha := {}
			_nValor := SD1->D1_TOTAL/SD1->D1_QUANT
			
	
			SF4->(dbSetOrder(1))
			SF4->(dbSeek(xFilial("SF4")+SD1->D1_TES,.F.))   // Busca TES Nota Fiscal Entrada para descobrir qual TES sera utilizado na Saida para devolucao do produto carcaca
			SF4->(dbSeek(xFilial("SF4")+SF4->F4_TESDV,.F.)) // Posiciona SF4 com o TES que sera utilizado para a saida (devolucao do produto recebido na nota fiscal entrada)
			SB1->(dbSeek(xFilial("SB1")+SD1->D1_COD,.F.))			
			
			If SB1->B1_TIPO == "MO"
			   lOOP
			Endif
			                                               
			// Adiciona Produto (É o item que será beneficiado, havera geração de OP a partir do PV para este item)
			aAdd(_aLinha,{'C6_FILIAL',xFilial("SC6"),NIL})
			aAdd(_aLinha,{'C6_NUM',SD1->D1_DOC,NIL})
			aAdd(_aLinha,{'C6_ITEM',StrZero(Len(_aSC6)+1,2,0),NIL}) //Item do pedido
			aAdd(_aLinha,{'C6_PRODUTO',SD1->D1_COD,NIL})
			aAdd(_aLinha,{'C6_QTDVEN',1,NIL})
			aAdd(_aLinha,{'C6_QTDLIB',0,NIL})		// Forca Qtde Liberada como 0, pois o sistema vai tratar isso posteriormente na liberacao da OP
			aAdd(_aLinha,{'C6_PRCVEN',SD1->D1_VUNIT,NIL})
			aAdd(_aLinha,{'C6_PRUNIT',SD1->D1_VUNIT,NIL})
			aAdd(_aLinha,{'C6_VALOR',_nValor,NIL})
			aAdd(_aLinha,{'C6_LOCAL',SD1->D1_LOCAL,NIL})
			aAdd(_aLinha,{'C6_NFORI',SD1->D1_DOC,NIL})
			aAdd(_aLinha,{'C6_SERIORI',SD1->D1_SERIE,NIL})
			aAdd(_aLinha,{'C6_ITEMORI',SD1->D1_ITEM,NIL})
			aAdd(_aLinha,{'C6_IDENTB6',SD1->D1_IDENTB6,NIL})
			aAdd(_aLinha,{'C6_ENTREG',SD1->D1_DTENTRE,NIL})
			aAdd(_aLinha,{'C6_TES',SF4->F4_CODIGO,NIL})
			aAdd(_aLinha,{'C6_CF',SF4->F4_CF,NIL})				
	  		aAdd(_aLinha,{'C6_ITPVSRV',StrZero(Len(_aSC6)+2,2,0),NIL})
			
			aAdd(_aSC6,_aLinha)
					
			_cITOrig := Substr(SD1->D1_ITEM,3,2)
			
			SF4->(dbSeek(xFilial("SF4")+_cTSServ,.F.))
			SB1->(dbSeek(xFilial("SB1")+SD1->D1_SERVICO))
			
			IF ! Alltrim(SB1->B1_GRUPO) $ Alltrim(GetMV("MV_X_GRPSC")) //Grupo de produtos de servicos que nao conterao valor agragado
				_nValor  := U_BuscaPrcVenda(SD1->D1_SERVICO,SF1->F1_FORNECE,SF1->F1_LOJA)
			Else
				_nValor := 10.00 
			EndIF			

		 	_aLinha  := {} // zera array alinha para adicionar novo item ao SC6
				 	
	
			// Adiciona Servico "d1_servico" no Pedido de Venda, este item não será produzido OP
			aAdd(_aLinha,{'C6_FILIAL',xFilial("SC6"),NIL})
			aAdd(_aLinha,{'C6_NUM',SD1->D1_DOC,NIL})
			aAdd(_aLinha,{'C6_ITEM',StrZero(Len(_aSC6)+1,2,0),NIL}) //Item do pedido
			aAdd(_aLinha,{'C6_PRODUTO',SD1->D1_SERVICO,NIL})
			aAdd(_aLinha,{'C6_QTDVEN',1,NIL})
			aAdd(_aLinha,{'C6_QTDLIB',0,NIL})		// Forca Qtde Liberada como 0, pois o sistema vai tratar isso posteriormente na liberacao da OP
			aAdd(_aLinha,{'C6_PRCVEN',_nValor,NIL})
			aAdd(_aLinha,{'C6_PRUNIT',_nValor,NIL})
			aAdd(_aLinha,{'C6_VALOR',_nValor,NIL})
			aAdd(_aLinha,{'C6_LOCAL',SD1->D1_LOCAL,NIL})
			aAdd(_aLinha,{'C6_NFORI',"",NIL})
			aAdd(_aLinha,{'C6_SERIORI',"",NIL})
			aAdd(_aLinha,{'C6_ITEMORI',"",NIL})
			aAdd(_aLinha,{'C6_IDENTB6',"",NIL})
			aAdd(_aLinha,{'C6_NUMOP',SD1->D1_DOC,NIL}) // grava No.OP apenas para manter relacionamento do item PV
			aAdd(_aLinha,{'C6_ITEMOP',_cITOrig,NIL})   // que refere-se ao Produto Beneficiamento com seu Item PV
			aAdd(_aLinha,{'C6_TES',_cTSServ,NIL})      // serviço agregado. Na emissão da NF teem que sair juntos.
			aAdd(_aLinha,{'C6_CF',SF4->F4_CF,NIL})
	
			aAdd(_aSC6,_aLinha)
					
		Next
			
		SD1->(dbSkip())

	Enddo
	
	//-- Chamada da rotina automatica de inclusao dos Pedidos de Venda MATA410
	MsExecAuto({|x,y,z|Mata410(x,y,z)},_aSC5,_aSC6,3)
	
	//-- Verifica se houve algum erro
	IF lmsErroAuto
		IF Aviso("Pergunta","Pedido de venda nao gerado. Deseja visualizar o log?",{"Sim","Nao"},1,"Atencao")=1
			MostraErro()
		EndIF
	EndIf
	
EndIF

SD1->(RestArea(_aAreaSD1))
SB2->(RestArea(_aAreaSB2))
SF4->(RestArea(_aAreaSF4))
SA1->(RestArea(_aAreaSA1))
SA2->(RestArea(_aAreaSA2))
SE4->(RestArea(_aAreaSE4))
RestArea(_aArea)

Return